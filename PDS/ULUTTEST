*---------------------------------------------------------------------*
*                                                                     *
*        This TEST routine will return ALL units for ONLINE DASD      *
*        devices with a volser beginning with Z.                      *
*                                                                     *
*---------------------------------------------------------------------*
ULUTTEST #START ,                  Start Procedure                     *
               AMODE=31,           Execute In 31-Bit AMode             *
               RMODE=ANY,          Execute In Any RMode                *
               BASE=(R12),         Define Base Register                *
               COPY=YES,                                               *
               PREFIX=TST,         Define Variable Prefix              *
               WKDSECT=WRKTEST,    Define WorkArea Name                *
               LOC=BELOW,          Indicate 24-Bit WorkArea            *
               LEVEL=**TOOL**      Maintenance Level           #DD15008

         XC    WRKPARM,WRKPARM     Initialize Parameter List
TEST0100 DS    0H
         LA    R6,=CL6'Z*'         Testing Register Input
         LA    R7,WRKPARM          Testing Register Input

         ULUTSCAN DEVCLASS=DASD,   Search For DASD Devices Only        *
               STATUS=ONLINE,      Search For ONLINE Devices           *
               VOLSER=(R6),        Search For Volumes With "Z" Prefix  *
               LINKAGE=SYSTEM,     LINK To ULUTSCAN Routine            *
               MF=(E,(R7))
         LTR   R15,R15             Check For End Of UCBS
         BNZ   ULUTEXIT            B. If All Done

         MVI   WRKMSG,C' '
         MVC   WRKMSG+1(WRKMSGL-1),WRKMSG

         LR    R3,R1               A(Returned UCB)
         USING UCBOB,R3

         MVC   WRKDEV#,ULUDEVNC-ULUPARM+WRKPARM
         UNPK  WRKDEVT(9),UCBTYP(5)
         TR    WRKDEVT(8),TESTHEX-C'0'
         MVI   WRKDEVT+8,C' '
         MVC   WRKVOLS,UCBVOLI
         DROP  R3

         TPUT  WRKMSG,WRKMSGL      Send Message To Terminal
         B     TEST0100

ULUTEXIT #STOP ,                   RETURN TO CALLER

TESTHEX  DC    C'0123456789ABCDEF'

         #STARTWA ,
WRKPARM  DS    25F                 Work Area

WRKMSG   DS    0F
WRKDEV#  DS    CL4,CL1             Device Number
WRKDEVT  DS    CL8,CL1             Device Type
WRKVOLS  DS    CL6,CL1             Volume Serial
WRKMSGL  EQU   *-WRKMSG
         #STOPWA ,

         #DSECTS UCB
         ULUPARM ,                 ULUTSCAN Parameter List
         END   ,
