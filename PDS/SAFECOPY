SAFECOPY TITLE '- PROTECT AGAINST ISPF AND IEWL WHILE COMPRESSING'
*---------------------------------------------------------------------*
*                 |                                                   *
* S A F E C O P Y |           PROTECT COMPRESS AGAINST                *
*                 |       ISPF USERS AND THE LINKAGE EDITOR           *
*------------------                                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*    FUNCTION:  ISSUE ISPF/EDIT AND IEWL ENQ/RESERVES WHILE INVOKING  *
*               IEBCOPY TO COMPRESS A SYSUT1 FILE.                    *
*                                                                     *
*       PARMS:  NONE                                                  *
*                                                                     *
*  EXIT CODES:  SAME AS FOR IEBCOPY                                   *
*                                                                     *
*   REGISTERS:  R0 - WORK REGISTER                                    *
*               R1 - WORK REGISTER                                    *
*               R2 - WORK REGISTER                                    *
*               R3 - NOT USED                                         *
*               R4 - NOT USED                                         *
*               R5 - NOT USED                                         *
*               R6 - NOT USED                                         *
*               R7 - NOT USED                                         *
*               R8 - NOT USED                                         *
*               R9 - NOT USED                                         *
*              R10 - NOT USED                                         *
*              R11 - NOT USED                                         *
*              R12 - BASE REGISTER                                    *
*              R13 - SAVE AREA ADDRESS                                *
*              R14 - RETURN ADDRESS                                   *
*              R15 - RETURN CODE                                      *
*                                                                     *
*      MACROS: #DSECTS  #ENVIRON #SETRC   #START   #STARTWA #STOP     *
*              #STOPWA  DCB      DEQ      ENQ      LINK     RDJFCB    *
*              RESERVE  TIOTSCAN WTO                                  *
*                                                                     *
*  ATTRIBUTES: AUTHORIZED (SYSIEWLP QNAME REQUIRES AUTHORIZATION)     *
*                                                                     *
*        NOTE: SYSIEWLP ENQUEUE WILL BE BYPASSED IF ON TSO            *
*                                                                     *
*---------------------------------------------------------------------*

*---------------------------------------------------------------------*
*                                                                     *
*     HISTORY: #V001000 - JUN 15,1993 -  CREATION DATE                *
*              #V001002 - MAR  2,2012 -  ADD EAV SUPPORT      #DD12062*
*                                                                     *
*---------------------------------------------------------------------*

         ACONTROL OPTABLE(ZS3)     ASSEMBLY OPTCDE TABLE
         SPLEVEL  SET=6            SPECIFY OS/390 R2 MACRO FORMAT
         SYSSTATE ARCHLVL=2        ASSEMBLY REQUIRES Z/ARCHITECTURE
         SYSSTATE OSREL=ZOSV1R13   ASSEMBLY REQUIRES Z/OS 1.13 & HIGHER

SAFECOPY #START ,                  *** HOUSEKEEPING ***                *
               AMODE=CAP24,        EXECUTE IN 24-BIT ADDRESSING MODE   *
               APARS=,             DON'T GENERATE APAR SLOTS           *
               BASE=(R12),         DEFINE BASE REGISTER(S)             *
               COPY=YES,                                               *
               LOC=BELOW,          INDICATE 24-BIT WORKAREA            *
               PREFIX=SAF,         DEFINE MACRO LABEL PREFIX           *
               REG0=R0,            SAVE INPUT PARAMETER ADDRESS        *
               REG1=R1,            SAVE INPUT PARAMETER ADDRESS        *
               RMODE=24,           EXECUTE IN 24-BIT RESIDENCY MODE    *
               SP=0,               DEFINE WORKAREA SUBPOOL             *
               WKDSECT=SAFEWRK,    DEFINE WORKAREA NAME                *
               LEVEL=#V001R02      DEFINE SOFTWARE VERSION     #DD12062
         USING PSA,0

*---------------------------------------------------------------------*
*                                                                     *
*        DETERMINE EXECUTION ENVIRONMENT (BATCH OR TSO)               *
*                                                                     *
*---------------------------------------------------------------------*

         #ENVIRON BATCH=SAFE0010   B. IF BATCH JOB
         OI    WRK$FLAG,WRK$TSO    INCICATE "TSO" ENVIRONMENT

*---------------------------------------------------------------------*
*                                                                     *
*        FIND THE TIOT ENTRY AND SAVE THE UCB ADDRESS                 *
*        EXTRACT THE DSNAME FOR THE DD STATEMENT                      *
*                                                                     *
*---------------------------------------------------------------------*

SAFE0010 DS    0H
         LA    R2,DCBDDNAM-IHADCB+WRKDCB
         TIOTSCAN DDN=(R2),NOK=ERROR1
         USING TIOENTRY,R1         ADDRESS TIOT ENTRY
         SLR   R15,R15
         ICM   R15,7,TIOEFSRT      LOAD UCB ADDRESS
         ST    R15,WRKUCB@         SAVE UCB ADDRESS FOR "RESERVE"

         RDJFCB WRKDCB             COPY THE JFCB
         LTR   R15,R15             CHECK IF THE DD STATEMENT EXISTS
         BNZ   ERROR2              B. IF NOT

         MVC   WRKDSNAM,JFCBDSNM-INFMJFCB+WRKJFCB

*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE A RESERVE TO LOCK OUT ISPF USERS & THE LINKAGE EDITOR  *
*              A ENQUEUE TO LOCK OUT THE LINKAGE EDITOR               *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*        NOTE: THE LINKAGE EDITOR ISSUES AN ENQUEUE AND A RESERVE     *
*              TO PROTECT AGAINST MULTIPLE MACHINES, THAT MAY NOT     *
*              BE PROTECTED BY GRS OR A REPLACEMENT PRODUCT.          *
*                                                                     *
*---------------------------------------------------------------------*

         RESERVE ,MF=(E,WRKRISPF)  LOCK OUT ISPF USERS
         TM    WRK$FLAG,WRK$TSO    CHECK FOR "TSO" ENVIRONMENT
         BO    SAFE0020            B. IF YES (BYPASS SYSIEWL ENQUEUE)
         RESERVE ,MF=(E,WRKRLINK)  LOCK OUT THE LINKAGE EDITOR
         ENQ   ,MF=(E,WRKELINK)    LOCK OUT THE LINKAGE EDITOR

*---------------------------------------------------------------------*
*                                                                     *
*        INVOKE IEBCOPY TO COMPRESS THE LIBRARY                       *
*                                                                     *
*---------------------------------------------------------------------*

SAFE0020 DS    0H
         TM    WRK$FLAG,WRK$TSO    CHECK FOR "TSO" ENVIRONMENT
         BO    SAFE0030            B. IF YES

         LINK  EP=IEBCOPY,PARAM=(WRKPARM1,WRKPARM2),VL=1
         #SETRC (R15)              SET RETURN CODE
         B     SAFE0050            TERMINATE

SAFE0030 DS    0H
         LA    R0,WRKPARM1         LOAD OPTIONAL PARAMETERS ADDRESS
         LA    R1,WRKPARM2         LOAD DDNAME TABLE ADDRESS
         STM   R0,R1,WRKPLIST      SAVE PARAMETER ADDRESSES
         OI    WRKPLIST+4,X'80'    INDICATE END OF PARAMETER LIST

         L     R15,FLCCVT          LOAD CVT  ADDRESS
         L     R15,CVTTVT-CVT(,R15)     TSVT ADDRESS
         L     R15,TSVTASF-TSVT(,R15)   INTERFACE ROUTINE ADDRESS
         CALL  (15),(WRKTSRFL,     INVOKE SERVICE ROUTINE              *
               IEBCOPY,            - NAME OF PROGRAM                   *
               WRKPROGL,           - LENGTH OF PROGRAM NAME            *
               WRKRETCD,           - RETURN CODE AREA                  *
               WRKRSNCD,           - REASON CODE AREA                  *
               WRKABNCD,           - ABEND  CODE AREA                  *
               WRKPLIST),          - PROGRAM PARAMETERS ADDRESS        *
               VL,MF=(E,WRKTSRPL)
         LTR   R15,R15             CHECK FOR ANY ERRORS
         BZ    SAFE0040            B. IF NOT
         ICM   R15,15,WRKRETCD     LOAD IEBCOPY RETURN CODE
         BNZ   SAFE0040            B. IF RETURN CODE FOUND
         L     R15,WRKABNCD        ELSE. LOAD IEBCOPY ABEND CODE
SAFE0040 DS    0H
         #SETRC (R15)              SET RETURN CODE
         B     SAFE0050            TERMINATE

*---------------------------------------------------------------------*
*                                                                     *
*        RELEASE PREVIOUS ENQUEUES AND RESERVES                       *
*                                                                     *
*---------------------------------------------------------------------*

SAFE0050 DS    0H
         DEQ   MF=(E,WRKDISPF)     RELEASE ISPF LOCKOUT
         TM    WRK$FLAG,WRK$TSO    CHECK FOR "TSO" ENVIRONMENT
         BO    SAFEEXIT            B. IF YES (BYPASS SYSIEWL ENQUEUE)
         DEQ   MF=(E,WRKDLINK)     RELEASE ISPF LOCKOUT

*---------------------------------------------------------------------*
*                                                                     *
*        TERMINATE                                                    *
*                                                                     *
*---------------------------------------------------------------------*

SAFEEXIT #STOP ,                   RETURN TO CALLER

*---------------------------------------------------------------------*
*                                                                     *
*        ERROR ROUTINES & MESSAGES                                    *
*                                                                     *
*---------------------------------------------------------------------*

ERROR1   DS    0H
         MVC   MESSAGE1+4(8),DCBDDNAM-IHADCB+WRKDCB
         WTO   MF=(E,MESSAGE1)
         EX    0,*
ERROR2   DS    0H
         WTO   MF=(E,MESSAGE2)
         EX    0,*
ERROR3   DS    0H
         WTO   MF=(E,MESSAGE3)
         EX    0,*

MESSAGE1 WTO   'XXXXXXXX DD STATEMENT NOT FOUND',ROUTCDE=11,MF=L
MESSAGE2 WTO   'INTERNAL ERROR - JFCB EXTRACT FAILED',ROUTCDE=11,MF=L
MESSAGE3 WTO   'INTERNAL ERROR - IEBCOPY ATTACH FAILED',ROUTCDE=11,MF=L

*---------------------------------------------------------------------*
*                                                                     *
*        CONSTANTS AND WORK AREA                                      *
*                                                                     *
*---------------------------------------------------------------------*

IEBCOPY  DC    CL8'IEBCOPY'
SPFEDIT  DC    CL8'SPFEDIT'
SYSIEWLP DC    CL8'SYSIEWLP'
WRK$FLAG DC    X'00'                 FLAG BYTE
WRK$TSO  EQU   X'80'      1... ....  - EXECUTING IN "TSO" ENVIRONMENT
WRKDCB   DCB   DDNAME=SYSUT1,DSORG=PS,MACRF=(R),EXLST=WRKEXLST,        *
               DCBE=WRKDCBE                                    #DD12062
WRKDCBE  DCBE  EADSCB=OK                                       #DD12062
WRKDISPF DEQ   (SPFEDIT,WRKDSNAM,44,SYSTEMS),                          *
               RET=HAVE,MF=L
WRKDLINK DEQ   (SYSIEWLP,WRKDSNAM,44,SYSTEM,                           *
               SYSIEWLP,WRKDSNAM,44,SYSTEMS),                          *
               RET=HAVE,MF=L
WRKDSNAM DS    CL44
WRKELINK ENQ   (SYSIEWLP,WRKDSNAM,E,44,SYSTEM),MF=L
WRKEXLST DC    X'87',AL3(WRKJFCB)
WRKJFCB  DS    0F,CL265
WRKPARM1 DS    0D                    NULL PARAMETER LIST
         DC    H'00',F'0'
WRKPARM2 DS    0D                    DDNAME OVERIDE TABLE
         DC    AL2(11*8)             LENGTH OF ALL ENTRIES
         DC    XL8'00'               DUMMY ENTRY
         DC    XL8'00'               DUMMY ENTRY
         DC    XL8'00'               DUMMY ENTRY
         DC    XL8'00'               DUMMY ENTRY
         DC    CL8'SYSIN'            SYSIN
         DC    CL8'SYSPRINT'         SYSPRINT
         DC    XL8'00'               DUMMY ENTRY
         DC    CL8'SYSUT1'           SYSUT1
         DC    CL8'SYSUT1'           SYSUT2 (CHANGED TO SYSUT1)
         DC    CL8'SYSUT3'           SYSUT3
         DC    CL8'SYSUT4'           SYSUT4
WRKRISPF RESERVE (SPFEDIT,WRKDSNAM,E,44,SYSTEMS),UCB=WRKUCB@,MF=L
WRKRLINK RESERVE (SYSIEWLP,WRKDSNAM,E,44,SYSTEMS),UCB=WRKUCB@,MF=L
WRKUCB@  DS    F
WRKTSRFL DS    0F                  TSO SERVICE ROUTINE FLAGS
         DC    H'0'                UNUSED
         DC    X'01'               DUMP IF PROGRAM ABENDS
         DC    X'02'               PROGRAM IS BEING INVOKED
WRKPROGL DC    A(L'IEBCOPY)        LENGTH OF PROGRAM NAME
WRKRETCD DC    F'0'                IEBCOPY RETURN CODE
WRKRSNCD DC    F'0'                TSO SERVICE ROUTINE REASON CODE
WRKABNCD DC    F'0'                IEBCOPY ABEND  CODE
WRKTSRCD DC    F'0'                TSO SERVICE ROUTINE RETURN CODE
WRKPLIST DS    2F                  IEBCOPY PARAMETER LIST
WRKTSRPL DS    7F                  TSO SERVICE ROUTINE PARAMETERS

         #STARTWA ,
         #STOPWA ,

*---------------------------------------------------------------------*
*                                                                     *
*        DSECTS                                                       *
*                                                                     *
*---------------------------------------------------------------------*

         #DSECTS CVT,DCB,JFCB,TIOT,PSA
         IKJTSVT ,                 TSO SERVICE VECTOR TABLE
         END   ,
