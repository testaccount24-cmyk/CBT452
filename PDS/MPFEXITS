MPFEXITS TITLE '- MPFEXITS - Display Loaded MPF Exits'
MPFEXITS #START ,                  Start Procedure                     *
               AMODE=31,           Execute In 31-Bit AMode             *
               RMODE=24,           Execute In 24-Bit RMode             *
               BASE=RELATIVE,      Generate Baseless Code              *
               CONSTANT=R12,       Base For Constants                  *
               COPY=YES,                                               *
               WKDSECT=WRKAREA,    Set WorkArea Name                   *
               LOC=BELOW,          Indicate 24-Bit Workarea Storage    *
               LEVEL=**TOOL**      Maintenance Level           #DD13234
         USING PSA,0

         L     R5,FLCCVT           A(CVT)
         USING CVT,R5
         ICM   R5,15,CVTCUCB       A(UCM)
         BZ    MPFLEXIT
         USING UCM,R5
         ICM   R5,15,UCMBFEXT
         BZ    MPFLEXIT
         USING UCMFEXTA,R5
         ICM   R5,15,UCMFMPFP      A(MPF Table)
         BZ    MPFLEXIT
         USING MPFT,R5
         SLR   R6,R6
         ICM   R6,3,MPFTNENM       #(Elements In MPF Exit Table)
         BZ    MPFLEXIT            B. If None
         ICM   R5,15,MPFTMENP      A(1st Element Of Exit Table)
         BZ    MPFLEXIT
         USING MPFMENTY,R5
LOOP     DS    0H
         MVC   INSERT1,MPFMNAME
         UNPK  INSERT2(9),MPFMADDR(5)
         TR    INSERT2(8),HEXTABLE
         MVI   INSERT2+8,C')'
         UNPK  INSERT3(9),MPFMLEN(5)
         TR    INSERT3(8),HEXTABLE
         MVI   INSERT3+8,C')'
         UNPK  INSERT4(9),MPFMEPA(5)
         TR    INSERT4(8),HEXTABLE
         MVI   INSERT4+8,C')'

         LA    R2,MESSAGE
         LHI   R3,L'MESSAGE
         TPUT  (R2),(R3)

         AHI   R5,MPFTENTY_LEN     A(Next Element In Exit Table)
         BRCT  R6,LOOP             Process All Table Entries

MPFLEXIT #STOP ,                   Return To Caller

         #CON  START               Beginning Of Constants
         #EXEC ,
         LTORG ,
MESSAGE  DC    C'________ ADDR(________) LEN(________) EPA(________) '
INSERT1  EQU   MESSAGE+00,8        INSERT ADDRESS/LENGTH
INSERT2  EQU   MESSAGE+14,8        INSERT ADDRESS/LENGTH
INSERT3  EQU   MESSAGE+28,8        INSERT ADDRESS/LENGTH
INSERT4  EQU   MESSAGE+42,8        INSERT ADDRESS/LENGTH
HEXTABLE DC    256X'00'
         ORG   HEXTABLE+C'0'
         DC    C'0123456789ABCDEF'
         ORG   ,
         #CON  END                 End Of Constants

         #STARTWA ,                Beginning Of WorkArea
         #STOPWA ,                 End Of WorkArea

         IEEZB809 ,
         #DSECTS CVT,UCM,PSA
         END   ,
