TOOL     TITLE '- ULUTSCAN - UCB Scan using ULUT table'
*---------------------------------------------------------------------*
*                 |                                                   *
* U L U T S C A N |         General UCB Scan Routine using            *
*                 |         The "ULUT" Table.                         *
*------------------                                                   *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*    Function: Scan the "ULUT" table one entry at a time.             *
*                                                                     *
*        Note: A long time ago Gilber Saint-Flour discovered the      *
*              ULUT table and used it to create an unauthorized       *
*              method of scanning the UCBs.  In z/OS 2.2 the ULUT     *
*              was moved to 64-bit storage.                           *
*              This routine continues Gilbert's legacy.               *
*                                                                     *
*              Be aware that the UCBs are NOT pinned and could be     *
*              relocated at any time if the IODF is updated during    *
*              the scan.                                              *
*                                                                     *
*              Read additional notes on the ULUT in CBT file 731.     *
*                                                                     *
*       Parms: R1 = Work Area Address                                 *
*                                                                     *
*  Exit Codes:  0 - UCB Found and returned in R1                      *
*               8 - UCB Not Found                                     *
*                                                                     *
* Register Usage:   R0 - Work Register                                *
*                   R1 - Work Register                                *
*                   R2 - Work Register                                *
*                   R3 - Work Register                                *
*                   R4 - Work Register                                *
*                   R5 - Current UCB Address                          *
*                   R6 - Scan Return Code                             *
*                   R7 - Not Used                                     *
*                   R8 - Input Parameter List                         *
*                   R9 - Not Used                                     *
*                  R10 - Not Used                                     *
*                  R11 - Not Used                                     *
*                  R12 - Constants Base Register                      *
*                  R13 - Work Area Base Register                      *
*                  R14 - Return Address                               *
*                  R15 - Return Code                                  *
*                                                                     *
*    Macros: #CON     #DSECTS  #START   #STARTWA #STOP    #STOPWA     *
*            ASAXWC   CVT      ECVT     IECDIOCM IEFUCBOB IOSDIOVT    *
*            IOSDULUT IOSDULU2 IOSDULU3 STORAGE                       *
*                                                                     *
*---------------------------------------------------------------------*

*---------------------------------------------------------------------*
*                                                                     *
*     History: #DD15008 - Jan 08,2015 -  Created.                     *
*                                                                     *
*---------------------------------------------------------------------*

         SYSSTATE AMODE64=YES      Have The Macros Expand Correctly

ULUTSCAN #START ,                  Start Routine                       *
               AMODE=31,           . Amode(31)                         *
               RMODE=ANY,          . Rmode(any)                        *
               BASE=RELATIVE,      . Use Relative Addressing           *
               CONSTANT=R12,       . Base For Constants And Literals   *
               COPY=YES,                                               *
               REG1=R8,            . Save Passed R1                    *
               USING=(ULUPARM,R8), . Address Parameter List Area       *
               WKDSECT=WRKSCAN,    . WorkArea Name                     *
               LOC=BELOW,          . Obtain 24-bit WorkArea Storage    *
               STORAGE=Y,          . Use "Storage" vs "Getmain"        *
               LEVEL=#V015008      Maintenance Level           #DD15008
         USING PSA,0

         SAM64 ,                   Ensure 64-Bit Mode

         LLGTR R12,R12             Ensure Clean 64-Bit Constant Base
         LLGTR R13,R13             Ensure Clean 64-Bit Workarea Base
         LLGTR R8,R8               Ensure Clean 64-Bit Parameter Base

         SLGR  R6,R6               Initialize Return Code

         LLGF  R2,FLCCVT           A(CVT)
         USING CVT,R2
         LLGF  R3,CVTIXAVL         A(IOCOM)
         USING IOCOM,R3
         LLGF  R3,IOCIOVTP         A(IOS Vector Table)
         USING IOVT,R3

         MVC   WRKDEVN,ULUDEVN     Save Starting Device Number
         MVC   ULUDEVNC,BLANKS     Initialize Output Character UCB

         LLGF  R3,IOVTUL31         UCB Lookup Table (31-Bit)
         LTGR  R3,R3               Check For 31-Bit ULUT
         JNZ   SCAN0100            B. If 31-Bit Table Exists
         LG    R3,IOVTULUT         UCB Lookup Table (64-Bit)

SCAN0100 DS    0H
         CLC   0(4,R3),=C'ULUT'    Check Eyecatcher
         JE    SCAN0300            B. If Acceptable
         J     SCAN2400            Else. Invalid ULUT
         USING ULUT,R3
SCAN0300 DS    0H
         CLI   ULUTVERS,1          Check For Type 1 Lookup Table
         JE    SCAN0400
         CLI   ULUTVERS,2          Check For Type 2 Lookup Table
         JE    SCAN0500
         CLI   ULUTVERS,3          Check For Type 3 Lookup Table
         JE    SCAN0600
         J     SCAN2400            Else. Invalid ULUT
         DROP  R3

*        Type 1 ULUT Processing
         USING ULUT,R3
SCAN0400 DS    0H
         LG    R0,ULUCOUNT         #(Previous Entries)
         LG    R1,ULUUCB           A(Previous Table Entry)
         CG    R1,ZEROS            Check If Scan In Progress
         JNZ   SCAN2100            B. If Yes
         LLGF  R4,ULUTUCBC         #(Table Entries)
         LLGF  R3,ULUT1ST          A(1st Table Entry)
         J     SCAN0700
         DROP  R3

*        Type 2 ULUT Processing
         USING ULU2,R3
SCAN0500 DS    0H
         LG    R0,ULUCOUNT         #(Previous Entries)
         LG    R1,ULUUCB           A(Previous Table Entry)
         CG    R1,ZEROS            Check If Scan In Progress
         JNZ   SCAN2100            B. If Yes
         LLGF  R4,ULU2UCBC         #(Table Entries)
         LLGF  R3,ULU21ST          A(1st Table Entry)
         J     SCAN0700
         DROP  R3

*        Type 3 ULUT Processing
         USING ULU3,R3
SCAN0600 DS    0H
         LG    R0,ULUCOUNT         #(Previous Entries)
         LG    R1,ULUUCB           A(Previous Table Entry)
         CG    R1,ZEROS            Check If Scan In Progress
         JNZ   SCAN2100            B. If Yes
         LLGF  R4,ULU3UCBC         #(Table Entries)
         LG    R3,ULU31ST          A(1st Table Entry)
         DROP  R3

*-------
*        Check For User Specified Filtering
*-------
         USING ULUENTRY,R3
SCAN0700 DS    0H
         SLGR  R5,R5
         ICM   R5,15,ULUEUCBP      A(This Unit's UCB)
         JZ    SCAN2300
         USING UCBOB,R5

         CLC   UCBCHAN,ULUDEVN     Check For Starting Device Number
         JL    SCAN2200            B. If Current Is Too Low

         CLI   ULUFLAG1,0          Check For Status/Device Filtering
         JNE   SCAN0800            B. If Yes
         TM    ULUVOL,X'BF'        Check For VOLSER= Filtering
         JNZ   SCAN0800            B. If Yes
         J     SCAN2500            Else. No Filtering

*        STATUS=ONLINE
SCAN0800 DS    0H
         TM    ULUFLAG1,ULU$ONL    Check For "Online" Filter
         JZ    SCAN0900            B. If Not
         TM    UCBSTAT,UCBONLI     Check If Device Is Online
         JZ    SCAN2200            B. If Not

*        DEVCLASS=TAPE
SCAN0900 DS    0H
         TM    ULUFLAG1,ULU$ALL    Check For Any DEVCLASS Filter
         JZ    SCAN1600            B. If Not
         TM    ULUFLAG1,ULU$TAPE   Check For "Tape" Filter
         JZ    SCAN1000            B. If Not
         TM    UCBTBYT3,UCB3TAPE   Check For Tape Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=COMM
SCAN1000 DS    0H
         TM    ULUFLAG1,ULU$COMM   Check For "Comm" Filter
         JZ    SCAN1100            B. If Not
         TM    UCBTBYT3,UCB3COMM   Check For Communications Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=DASD
SCAN1100 DS    0H
         TM    ULUFLAG1,ULU$DASD   Check For "DASD" Filter
         JZ    SCAN1200            B. If Not
         TM    UCBTBYT3,UCB3DACC   Check For DASD Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=DISP
SCAN1200 DS    0H
         TM    ULUFLAG1,ULU$DISP   Check For "Disp" Filter
         JZ    SCAN1300            B. If Not
         TM    UCBTBYT3,UCB3DISP   Check For Display Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=UREC
SCAN1300 DS    0H
         TM    ULUFLAG1,ULU$UREC   Check For "URec" Filter
         JZ    SCAN1400            B. If Not
         TM    UCBTBYT3,UCB3UREC   Check For Unit Record Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=CHAR
SCAN1400 DS    0H
         TM    ULUFLAG1,ULU$UREC   Check For "Char" Filter
         JZ    SCAN1500            B. If Not
         TM    UCBTBYT3,UCB3CHAR   Check For Character Reader Device
         JZ    SCAN2200            B. If Not
*        DEVCLASS=CTC
SCAN1500 DS    0H
         TM    ULUFLAG1,ULU$CTC    Check For "CTC" Filter
         JZ    SCAN1600            B. If Not
         TM    UCBTBYT3,UCB3CTC    Check For Channel-To-Channel Device
         JZ    SCAN2200            B. If Not

*        VOLSER=
SCAN1600 DS    0H
         TM    ULUVOL,X'BF'        Check For "VOLSER=" Filter
         JZ    SCAN2500            B. If Not
         TM    UCBVOLI,X'BF'       Check If UCB Has A Volser
         JZ    SCAN2200            B. If Not
         SAM31 ,                   Ensure 31-Bit Mode

         LA    R0,ULUVOL           A(Start Of VOLSER)
         LA    R1,ULUVOL+5         A(Last Byte Of VOLSER)
SCAN1700 DS    0H
         TM    0(R1),X'BF'         Check For End Of Volser
         JNZ   SCAN1800            B. If End Found
         BRCT  R1,SCAN1700         Loop Till End Found
SCAN1800 DS    0H
         AHI   R1,1                A(End Of Volser)
         SR    R1,R0               Calculate Length
         ST    R1,WRKPLEN          Save Pattern Volser Length

         LA    R0,UCBVOLI          A(Start Of VOLSER)
         LA    R1,UCBVOLI+5        A(Last Byte Of VOLSER)
SCAN1900 DS    0H
         TM    0(R1),X'BF'         Check For End Of Volser
         JNZ   SCAN2000            B. If End Found
         BRCT  R1,SCAN1900         Loop Till End Found
SCAN2000 DS    0H
         AHI   R1,1                A(End Of Volser)
         SR    R1,R0               Calculate Length
         ST    R1,WRKSLEN          Save String Volser Length

         ASAXWC PATTERNSTR=ULUVOL,                                     *
               PATTERNSTRLEN=WRKPLEN,                                  *
               STRING=UCBVOLI,                                         *
               STRINGLEN=WRKSLEN,                                      *
               ZEROORMORE=STAR,                                        *
               ONECHAR=ANYCHAR,                                        *
               DELIMITER=LENGTHS_WERE_SET,                             *
               RETCODE=WRKRCODE,                                       *
               WORKAREA=WRKPATWA,                                      *
               MF=(E,WKASAXWC)

         SAM64 ,                   Ensure 64-Bit Mode
         LLGF  R15,WRKRCODE        Load Return Code
         LTR   R15,R15             Check For Matching Volser / Mask
         JNZ   SCAN2200            B. If Not
         J     SCAN2500

SCAN2100 DS    0H
         LGR   R3,R1               A(Previous ULUT Entry)
         LGR   R4,R0               #(Previous ULUT Entries)

SCAN2200 DS    0H
         LA    R3,ULUELEN(,R3)     A(Next ULUT Entry)
         BRCT  R4,SCAN0700         Loop Till End Of UCBs

SCAN2300 DS    0H
         SLGR  R3,R3
         SLGR  R4,R4
         SLGR  R6,R6
         LHI   R6,8                Indicate End-Of-UCBs
         J     SCANEXIT

SCAN2400 DS    0H
         SLGR  R3,R3
         SLGR  R4,R4
         SLGR  R6,R6
         LHI   R6,12               Indicate Invalid "ULUT"
         J     SCANEXIT

SCAN2500 DS    0H
         MVC   ULUDEVN,UCBCHAN     Save Device Number (Hex)
         UNPK  WRKDBL1(5),UCBCHAN(3)
         TR    WRKDBL1(4),HEXTABLE
         MVC   ULUDEVNC,WRKDBL1    Save Device Number (Char)

SCANEXIT DS    0H
         STG   R3,ULUUCB
         STG   R4,ULUCOUNT

         ST    R6,ULURETCD
         DROP  R3,R5

         SAM31 ,                   Ensure 31-Bit Mode
         #STOP R0=(R4),R1=(R5),RC=(R6)

         #CON  START               Constants Area
         LTORG ,
ANYCHAR  DC    CL1'?'              Mask - Single Character
BLANKS   DC    CL8' '
HEXTABLE DC    256x'00'
         ORG   HEXTABLE+C'0'
         DC    C'0123456789ABCDEF' HEX Conversion Table
         ORG   ,
STAR     DC    CL1'*'              Mask - One Or More
ZEROS    DC    D'0'                Zeros
         #CON  END

         #STARTWA ,                Dynamic WorkArea
         ASAXWC MF=(L,WKASAXWC)    ASAXWC Parameter List
WRKDBL1  DS    D                   DBLWORD Work Area
WRKDBL2  DS    D                   DBLWORD Work Area
WRKDEVN  DS    H                   Input Starting Device Number
WRKPLEN  DS    F                   ASAXWC Pattern Length
WRKSLEN  DS    F                   ASAXWC String Length
WRKRCODE DS    F                   ASAXWC Return Code
WRKPATWA DS    XL256               ASAXWC Work Area
         #STOPWA ,

         ULUPARM ,                 ULUTSCAN Parameter List

         #DSECTS CVT,IOCOM,PSA,UCB,LIST=YES
         AIF   (D'CVTZOS_V2R2).GOTCVT  Check If Already Defined
CVTZOS_V2R2   EQU X'40'      z/OS V2R2                             @NQA
.GOTCVT  ANOP  ,
         IOSDIOVT ,                IOVT Mapping
         IOSDULUT ,                UCB Look-Up Table
         IOSDULU2 ,                UCB Look-Up Table (Version 2)
         IOSDULU3 ,                UCB Look-Up Table (Version 3)
         END   ,
