CATEXTR  #START BASE=(R12),AMODE=31,RMODE=24,COPY=YES

*---------------------------------------------------------------------*
*        CHECK FOR PARM=CATACB                                        *
*---------------------------------------------------------------------*
         L     R1,0(,R1)           LOAD INPUT PARAMETER LIST ADDRESS
         SLR   R2,R2
         ICM   R2,3,0(R1)          LOAD INPUT PARAMETER TEXT LENGTH
         BZ    CATE0100            B. IF NONE SPECIFIED
         CH    R2,=H'06'           CHECK FOR MAXIMUM LENGTH
         BH    CATE0100            B. IF TOO LONG
         BCTR  R2,0                DECREMENT FOR EXECUTE
         #EXEC R2,CLC,2(*-*,R1),=CL6'CATACB'
         BNE   CATE0100            B. IF PARM IS NOT "CATACB"
         OI    WRKFLAG,WRK$ACB@    ELSE. REMEMBER "CATACB" SPECIFIED

*---------------------------------------------------------------------*
*        SAVE INPUT DATASET NAME                                      *
*---------------------------------------------------------------------*
CATE0100 DS    0H
         LOAD  EP=HEXDUMP          LOAD THE HEX DUMP FORMATTER
         ST    R0,WRKDUMP@         SAVE "HEXDUMP" ROUTINE ADDRESS

         L     R2,WRKAREAL         LOAD WORKAREA LENGTH
         GETMAIN RU,LV=(R2)        ACQUIRE STORAGE FOR WORKAREA
         ST    R1,WRKAREA          SAVE WORKAREA ADDRESS

         LA    R0,WRKDUMPP+4       LOAD HEXDUMP PARAMETER LIST ADDRESS
         ST    R0,WRKDUMPP
         MVI   WRKDUMPP+16,X'40'   INDICATE RELATIVE ADDRESS
         CLC   DCBLRECL-IHADCB+SYSPRINT,=AL2(133)
         BL    CATE0150            B. IF OUTPUT RECORD LENGTH < 133
         MVI   WRKDUMPP+16,X'20'   INDICATE 130 BYTE DUMP OUTPUT

CATE0150 DS    0H
         LA    R0,WRKPRINT+8       LOAD HEXDUMP OUTPUT AREA ADDRESS
         STCM  R0,7,WRKDUMPP+17
         MVC   WRKPRINT,BLANKS     INITIALIZE THE OUTPUT BUFFER

         OPEN  (SYSIN,(INPUT),SYSPRINT,(OUTPUT))

*---------------------------------------------------------------------*
*        GET NEXT INPUT RECORD AND CHECK FOR NEW DATASET NAME         *
*---------------------------------------------------------------------*
CATE0200 DS    0H
         GET   SYSIN               GET INPUT RECORD
         LR    R10,R1              SAVE RECORD ADDRESS

         CLI   0(R10),C'*'         CHECK FOR COMMENT RECORD
         BE    CATE0200            B. IF YES
         OC    0(80,R10),BLANKS    ENSURE UPPER CASE DATA

         CLC   =C'DSN=',0(R10)     CHECK FOR "DSN="
         BNE   CATE0400            B. IF NOT
         XC    WRKACB@,WRKACB@
         MVC   WRKDSNAM,4(R10)     SAVE DATASET NAME

         MVC   WRKPRINT+1(72),0(R10)
         PUT   SYSPRINT,WRKPRINT   PRINT CURRENT INPUT RECORD
         MVC   WRKPRINT,BLANKS

         TM    WRKFLAG,WRK$ACB@    CHECK FOR PARM=CATACB
         BNO   CATE0200            B. IF NOT

         XC    WRKCTGFL,WRKCTGFL
         #CTGFL CATACB,MF=(E,WRKCTGFL)
         XC    WRKCTGPL,WRKCTGPL
         L     R2,WRKAREA          LOAD WORKAREA ADDRESS
         L     R3,WRKAREAL         LOAD WORKAREA LENGTH
         #CTGPL LISTCAT,                                               *
               OPTIONS=(NAME,BOTH,AM0),                                *
               CRI=WRKDSNAM,                                           *
               WORK=((R2),(R3),FORMAT2),                               *
               FIELDS=(WRKCTGFL),                                      *
               MF=(E,WRKCTGPL)

         CATALOG WRKCTGPL
         LTR   R15,R15
         BZ    CATE0300
         #SETRC (R15),FIELD=CATRC
         MVC   WRKMSG01+1(8),=CL8'CATACB'
         LA    R14,WRKCTGPL
         USING CTGPL,R14
         MVC   WRKMSG01+32(2),CTGMODID
         STM   R15,R0,WRKDBL1      SAVE RETURN CODE / REASON CODE
         MVI   WRKMSG01+42,C''''
         UNPK  WRKMSG01+43(9),WRKDBL1(5)
         TR    WRKMSG01+43(8),WRKTABLE-C'0'
         MVI   WRKMSG01+51,C''''
         MVI   WRKMSG01+61,C''''
         UNPK  WRKMSG01+62(5),CTGREASN(3)
         DROP  R14
         TR    WRKMSG01+62(4),WRKTABLE-C'0'
         MVI   WRKMSG01+66,C''''
         PUT   SYSPRINT,WRKMSG01
         B     CATE0200
CATE0300 DS    0H
         #GETFL WRKCTGFL,(R2),(R3),ZERO=CATE0200
         MVC   WRKACB@,0(R2)
         B     CATE0200

*---------------------------------------------------------------------*
*        FORMAT FIELD PARAMETER LIST                                  *
*---------------------------------------------------------------------*
CATE0400 DS    0H
         XC    WRKCTGFL,WRKCTGFL
         #CTGFL (R10),MF=(E,WRKCTGFL)

*---------------------------------------------------------------------*
*        FORMAT CATALOG PARAMETER LIST                                *
*---------------------------------------------------------------------*
         XC    WRKCTGPL,WRKCTGPL
         L     R2,WRKAREA          LOAD WORKAREA ADDRESS
         L     R3,WRKAREAL         LOAD WORKAREA LENGTH
         ICM   R15,15,WRKACB@      CHECK IF A CATALOG'S ACB EXISTS
         BZ    CATE0500            B. IF NOT
         #CTGPL LISTCAT,                                               *
               OPTIONS=(NAME,BOTH,AM0),                                *
               CRI=WRKDSNAM,                                           *
               CATALOG=WRKACB@,                                        *
               WORK=((R2),(R3)),                                       *
               FIELDS=(WRKCTGFL),                                      *
               MF=(E,WRKCTGPL)
         B     CATE0600

CATE0500 DS    0H
         #CTGPL LISTCAT,                                               *
               OPTIONS=(NAME,BOTH,AM0),                                *
               CRI=WRKDSNAM,                                           *
               WORK=((R2),(R3)),                                       *
               FIELDS=(WRKCTGFL),                                      *
               MF=(E,WRKCTGPL)

CATE0600 DS    0H
         CATALOG WRKCTGPL
         LTR   R15,R15
         BZ    CATE0700
         #SETRC (R15),FIELD=CATRC
         MVC   WRKMSG01+1(8),0(R10)
         LA    R14,WRKCTGPL
         USING CTGPL,R14
         MVC   WRKMSG01+32(2),CTGMODID
         STM   R15,R0,WRKDBL1      SAVE RETURN CODE / REASON CODE
         MVI   WRKMSG01+42,C''''
         UNPK  WRKMSG01+43(9),WRKDBL1(5)
         TR    WRKMSG01+43(8),WRKTABLE-C'0'
         MVI   WRKMSG01+51,C''''
         MVI   WRKMSG01+61,C''''
         UNPK  WRKMSG01+62(5),CTGREASN(3)
         DROP  R14
         TR    WRKMSG01+62(4),WRKTABLE-C'0'
         MVI   WRKMSG01+66,C''''
         PUT   SYSPRINT,WRKMSG01
         B     CATE0200

*---------------------------------------------------------------------*
*        REPORT ON EXTRACTED CATALOG FIELD'S INFORMATION              *
*---------------------------------------------------------------------*
CATE0700 DS    0H
         #GETFL WRKCTGFL,(R2),(R3),ZERO=CATE0800
         BAL   R14,DUMPIT          DUMP THE EXTRACTED FIELD INFORMATION
         B     CATE0200

CATE0800 DS    0H
         MVC   WRKPRINT+1(8),0(R10)
         PUT   SYSPRINT,WRKPRINT   PRINT CURRENT INPUT RECORD
         MVC   WRKPRINT,BLANKS
         B     CATE0200

*---------------------------------------------------------------------*
*        TERMINATE                                                    *
*---------------------------------------------------------------------*
CATE0900 DS    0H
         ICM   R15,15,WRKDUMP@     LOAD HEXDUMP ROUTINE ADDRESS
         BZ    CATEEXIT            B. IF ROUTINE NOT LOADED
         DELETE EP=HEXDUMP         DELETE THE HEX DUMP FORMATTER

CATEEXIT DS    0H
         CLOSE (SYSIN,,SYSPRINT)
         L     R15,CATRC
         #STOP RC=(R15)            TERMINATE

*---------------------------------------------------------------------*
*        EXTRACTED FIELD DUMPPING ROUTINE                             *
*---------------------------------------------------------------------*
DUMPIT   DS    0H
         STM   R0,R15,WRKSAVE      SAVE    ALL REGISTERS
         ST    R2,WRKDUMPP+4       INSERT 1ST BYTE ADDRESS
         BCTR  R3,0
         AR    R3,R2
         ST    R3,WRKDUMPP+8       INSERT LAST BYTE ADDRESS

         XC    WRKDUMPP+12(4),WRKDUMPP+12
         NI    WRKFLAG,255-WRK$NAME

DUMP0100 DS    0H
         LA    R1,WRKDUMPP         LOAD HEXDUMP ROUTINE PLIST ADDRESS
         ICM   R15,15,WRKDUMP@     LOAD HEXDUMP ROUTINE ADDRESS
         BZ    DUMPEXIT            B. IF ROUTINE NOT LOADED
         BALR  R14,R15             FORMAT A LINE OF DATA INTO HEX

         TM    WRKFLAG,WRK$NAME    CHECK IF 1ST LINE HAS BEEN DISPLAYED
         BO    DUMP0110            B. IF YES
         OI    WRKFLAG,WRK$NAME    REMEMBER 1ST LINE HAS BEEN DISPLAYED
         MVC   WRKPRINT+1(8),0(R10)

DUMP0110 DS    0H
         PUT   SYSPRINT,WRKPRINT   PRINT CURRENT LINE
         MVC   WRKPRINT,BLANKS
         CLC   WRKDUMPP+4(4),WRKDUMPP+8
         BNH   DUMP0100            B. IF MORE DATA TO BE DUMPED

DUMPEXIT DS    0H
         LM    R0,R15,WRKSAVE      RESTORE ALL REGISTERS
         BR    R14                 RETURN TO CALLER

*---------------------------------------------------------------------*
*        WORK AREA AND CONSTANTS                                      *
*---------------------------------------------------------------------*
         #EXEC ,
         LTORG ,

CATRC    DC    F'0'

BLANKS   DC    256C' '             LOTS OF BLANKS
DCBEXIT  #DCBEXIT LRECL=133        DCB OPEN EXIT
DCBEXIT@ DC    X'85',AL3(DCBEXIT)  DCB OPEN EXIT ADDRESS
SYSIN    DCB   DDNAME=SYSIN,DSORG=PS,RECFM=FB,LRECL=80,                *
               MACRF=GL,EODAD=CATE0900
SYSPRINT DCB   DDNAME=SYSPRINT,DSORG=PS,RECFM=FBA,                     *
               MACRF=PM,EXLST=DCBEXIT@

WRKACB@  DC    F'0'
WRKAREA  DC    A(*-*)
WRKAREAL DC    A(32767)
WRKCTGFL DC    XL(CTGFLLEN)'00'
WRKCTGPL DC    XL(CTGPLLEN+4*1)'00'
WRKDBL1  DC    D'0'                DOUBLEWORD WORK AREA
WRKDSNAM DC    CL44' '
WRKDUMP@ DC    A(*-*)              HEXDUMP ROUTINE ADDRESS
WRKDUMPP DC    A(*+4)              POINTER TO HEXDUMP PARAMETER LIST
         DS    A               +4  STARTING ADDRESS OF DUMP STORAGE
         DS    A               +8  LAST BYTE ADDRESS OF DUMP STORAGE
         DS    A               +12 RELATIVE ADDRESS (OPTIONAL)
         DS    X'40'           +16 DUMP OPTIONS (RELATIVE & 130 BYTES)
*              X'80'               - PRINT ABSOLUTE STORAGE ADDRESS
*              X'40'               - PRINT RELATIVE STORAGE ADDRESS
*              X'20'               - PRINT 130 BYTES PER LINE (ON)
*                                    PRINT  80 BYTES PER LINE (OFF)
         DS    AL3             +17 OUTPUT BUFFER ADDRESS
WRKFLAG  DC    X'00'              FLAG BYTE
WRK$ACB@ EQU   BIT0               PARM=CATACB WAS SPECIFIED
WRK$NAME EQU   BIT1               NAME HAS BEEN DISPLAYED
WRKMSG01 DC    CL134'          - *** ERROR *** IGG0CLXX - R15=X"#######*
               #" REASON=X"####"'
WRKPRINT DS    CL256               OUTPUT BUFFER
WRKSAVE  DC    18F'0'              SUBROUTINE SAVE AREA
WRKTABLE DC    C'0123456789ABCDEF' TRANSLATION TABLE

         #DSECTS CATLG,DCB
         END   ,
