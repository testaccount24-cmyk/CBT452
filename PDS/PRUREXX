/*-REXX/PRUREXX ------------------------------
   SYNTAX:   PRUREXX dsn {anychar}
   AUTHOR:   Oscar Omar Ortega Ortega
   OUTPUT:   dsn.NEW{.NEW{...}}
   NOTE:  change "userid.MVS-JES2.LOADLIB"

   This EXEC will create various datasets under the user's prefix.
   You may need to review this REXX if your installation has any
   file name or other restrictions.

  ---------------------------------------------*/
Trace O
address TSO
parse upper arg bib param
if bib = "" then do
   Say 'You must provide PDS name to process'
   exit
end
if param <> '' then param = "'ALL'"

upre1 = sysvar(SYSPREF)
upref = upre1                          /* to avoid second call later */
if upref = "" then upref = Userid()    /* either prefix or userid    */

apos1 = pos("'",bib)
if apos1 = 0 then do                       /* if it has no quotes    */
   if upre1 <> "" then bib = upref"."bib   /* append prefix or uid   */
   bib = "'"bib"'"
end

Exist = SYSDSN(bib)                    /* check dsn existance        */
If Exist <> 'OK'                       /* if not exists, say so      */
then do
   Say 'PDS 'bib' NOT found or not catalogued'
   exit
end

dsprin = "'"upref".PRUPGM.SYSPRINT'"           /* pru pgm sysprint   */

bib2 = bib
do forever
   totlng = length(bib2)                   /* check current length   */
   bib2 = left(bib2,totlng-1)              /* delete last quote      */
   bib2 = bib2".NEW'"                      /* append .NEW to dsn     */
   if totlng > 41 then call Change_Name    /* check if dsn > 44 char */
   Exist = sysdsn(bib2)
   If Exist <> 'OK'                        /* check dsn existance    */
   then leave                              /* if so, iterate         */
end

msgst =  MSG('OFF')
if sysdsn(dsprin) = 'OK' then "DELETE "dsprin /* if exists, delete it*/

x = LISTDSI(bib directory)                 /* to check if its a PDS  */
msgst =  MSG(msgst)

if sysdsorg <> 'PO' & sysdsorg <> 'POU' then do  /* if dsn not a PDS */
   Say 'Dataset 'bib' is NOT a PDS, it is 'sysdsorg
   exit
end

msgst =  MSG('OFF')
"FREE FI(SYSPRINT,INPUT,OUTPUT)"   /* include SYSUDUMP as optional   */
msgst =  MSG(msgst)

/*  allocate all work files       */

"ALLOC FI(SYSPRINT) DA("dsprin") REUSE UNIT(SYSDA) ",
       "SPACE(5 1) TRACKS RECFM(F,B,A) LRECL(133) BLK(27930)"
"ALLOC FI(INPUT)    DA("bib") SHR REUSE"
"ALLOC FI(OUTPUT)   DA("bib2") NEW UNIT(SYSDA) LIKE("bib") RELEASE"

"CALL 'userid.MVS-JES2.LOADLIB(PRU)' "param   /* call PRU program    */
RcPRU = rc                             /* save return code    */

"FREE FI(SYSPRINT,INPUT,OUTPUT)"   /* include SYSUDUMP as optional   */
"ALLOC FI(SYSPRINT) DA(*)"         /* reallocate SYSPRINT to terminal*/

select
   when RcPRU = 0 then do
      if sysvar("SYSISPF") = "ACTIVE"
      then address ISPEXEC "VIEW DATASET("dsprin")"
      else Say 'PDS 'bib' procesed with name 'bib2' successfully'
   end
   when RcPRU = 4 then do
      Say 'PDS 'bib' processed and no members were recovered'
      msgst =  MSG('OFF')
      "DELETE "dsprin
      "DELETE "bib2
      msgst =  MSG(msgst)
   end
   otherwise Say 'PDS 'bib' procesed with errors RC='RcPRU
end
exit

Change_Name:
 fecS = date('S')
 timN = time()
 timN = left(timN,2)substr(timN,4,2)right(timN,2)
 Say 'Output PDS 'bib2
 bib2 = "'"upref".PRUPGM.RECOVERD.D"right(fecS,6)".T"timN"'"
 Say 'changed to 'bib2' due to dsn length'
return
