RXLOCATE TITLE 'Return Absolute or True name'
*------------------------------------------------------------------*
*                                                                  *
*        To Call:                                                  *
*                                                                  *
*        /*rexx*/                                                  *
*        LOCDSN = 'prefix.gdg.name(-1)' or 'prefix.alias.name'     *
*        address LINK "RXLOCATE"                                   *
*        say LOCDSN   <- absolute or true name                     *
*        exit                                                      *
*                                                                  *
*------------------------------------------------------------------*

RXLOCATE CSECT ,
RXLOCATE AMODE 31
RXLOCATE RMODE ANY
         BAKR  14,0
         LARL  R12,RXLOCATE
         USING RXLOCATE,R12

         STORAGE OBTAIN,LENGTH=WRKLEN,LOC=ANY,SP=1,CALLRKY=YES
         LR    R13,R1
         USING WRKAREA,R13
         USING SHVBLOCK,WRKSHV

         MVI   WRKDSNAM,C' '
         MVC   WRKDSNAM+1(L'WRKDSNAM-1),WRKDSNAM

*        Fetch "LOCDSN" Variable
         XC    SHVBLOCK(SHVBLEN),SHVBLOCK
         MVI   SHVCODE,SHVFETCH
         LA    R0,VARNAME          A(Variable Name)
         ST    R0,SHVNAMA
         LHI   R0,L'VARNAME        L(Variable Name)
         ST    R0,SHVNAML
         LA    R0,WRKDSNAM         A(DSName Area)
         ST    R0,SHVVALA
         LHI   R0,L'WRKDSNAM       L(DSName Area)
         ST    R0,SHVBUFL
         LA    R2,IRXEXCOM
         LA    R4,SHVBLOCK
         LINK  EP=IRXEXCOM,PARAM=((R2),0,0,(R4)),VL=1
         LTR   R15,R15
         JNZ   EXIT

*        Locate The Catalog Entry
         MVC   WRKLOC(NAMEPLL),NAMEPL
         LA    R0,WRKDSNAM
         ST    R0,WRKLOC+4
         LA    R0,BUFFER
         ST    R0,WRKLOC+12
         LOCATE WRKLOC             Locate Catalog Entry
         LR    R5,R15

*        Store "LOCDSN" Variable
         XC    SHVBLOCK(SHVBLEN),SHVBLOCK
         MVI   SHVCODE,SHVSTORE
         LA    R0,VARNAME          A(Variable Name)
         ST    R0,SHVNAMA
         LHI   R0,L'VARNAME        L(Variable Name)
         ST    R0,SHVNAML
         LA    R0,WRKDSNAM         A(DSName Area)
         ST    R0,SHVVALA
         LHI   R0,L'WRKDSNAM       L(DSName Area)
         ST    R0,SHVBUFL
         ST    R0,SHVVALL
         LA    R2,IRXEXCOM
         LA    R4,SHVBLOCK
         LINK  EP=IRXEXCOM,PARAM=((R2),0,0,(R4)),VL=1
         LR    R15,R5

EXIT     DS    0H
         LR    R5,R15
         STORAGE RELEASE,LENGTH=WRKLEN,ADDR=(R13),SP=1,CALLRKY=YES
         LR    R15,R5
         PR    ,

ERROR1   DS    0H
         EX    0,*                 Abend S0C3

VARNAME  DC    C'LOCDSN'
IRXEXCOM DC    CL8'IRXEXCOM'

NAMEPL   CAMLST NAME,*-*,,*-*
NAMEPLL  EQU   *-NAMEPL

WRKAREA  DSECT ,
         DS    18F

WRKLOC   DS    XL(NAMEPLL)
WRKDSNAM DS    CL44
BUFFER   DS    CL265
WRKSHV   DS    0F,XL(SHVBLEN)

         DS    0D
WRKLEN   EQU   *-WRKAREA

         YREGS ,

* SHVBLOCK:  Shared-Variable Parameter List
SHVBLOCK DSECT
SHVNEXT  DS    A     CHAIN POINTER (0 IF LAST BLOCK)
SHVUSER  DS    F     AVAILABLE FOR PRIVATE USE, EXCEPT DURING
*                      "FETCH NEXT" WHEN IT IDENTIFIES THE
*                      LENGTH OF THE BUFFER POINTED TO BY SHVNAMA.
SHVCODE  DS    CL1   INDIVIDUAL FUNCTION CODE INDICATING
*                      THE TYPE OF VARIABLE ACCESS REQUEST
*                      (S,F,D,s,f,d,N, OR P)
SHVRET   DS    XL1   INDIVIDUAL RETURN CODE FLAGS
         DS    H'0'  RESERVED, SHOULD BE ZERO
SHVBUFL  DS    F     LENGTH OF 'FETCH' VALUE BUFFER
SHVNAMA  DS    A     ADDRESS OF VARIABLE NAME
SHVNAML  DS    F     LENGTH OF VARIABLE NAME
SHVVALA  DS    A     ADDRESS OF VALUE BUFFER
SHVVALL  DS    F     LENGTH OF VALUE
SHVBLEN  EQU   *-SHVBLOCK  (LENGTH OF THIS BLOCK = 32)
         SPACE
*
*     FUNCTION CODES (PLACED IN SHVCODE):
*
SHVFETCH EQU   C'F'  COPY VALUE OF VARIABLE TO BUFFER
SHVSTORE EQU   C'S'  SET VARIABLE FROM GIVEN VALUE
SHVDROPV EQU   C'D'  DROP VARIABLE
SHVSYSET EQU   C's'  SYMBOLIC NAME SET VARIABLE
SHVSYFET EQU   C'f'  SYMBOLIC NAME FETCH VARIABLE
SHVSYDRO EQU   C'd'  SYMBOLIC NAME DROP VARIABLE
SHVNEXTV EQU   C'N'  FETCH "NEXT" VARIABLE
SHVPRIV  EQU   C'P'  FETCH PRIVATE INFORMATION
         END   ,
