EDITSUB  TITLE '- Submit Currently Edited Data Set Without Saving'
*------------------------------------------------------------------*
*                                                                  *
*        This EDIT MACRO will submit the data that is currently    *
*        being edited, without saving the data first.              *
*                                                                  *
*        It also does not require a SPFTEMPn file as it writes     *
*        the data directly to the internal reader.                 *
*                                                                  *
*        As this is an initial initial version there may be        *
*        problems so please let me know if you encounter any.      *
*                                                                  *
*        To Execute:   !EDITSUB                                    *
*                                                                  *
*        To elimintate the "!", in an IMACRO define this macro:    *
*            DEFINE EDITSUB MACRO PGM                              *
*                                                                  *
*        Note:                                                     *
*        - The SUBMIT Exits Are NOT Called (ie. IKJEFF10).         *
*        - No "IKJ56250I JOB jobname(jobid) SUBMITTED" Message     *
*        - A single ISPF message indicating the "LAST" job number  *
*          that was submitted is displayed.                        *
*        - Probably more issues that I haven't encountered.        *
*                                                                  *
*        Please send all suggestions/updates to:                   *
*            zOS.JES2@Gmail.com                                    *
*                                                                  *
*------------------------------------------------------------------*

         ACONTROL OPTABLE(ZS3)     Assembly OPTCDE Table
         SPLEVEL  SET=6            Specify OS/390 R2 macro format
         SYSSTATE ARCHLVL=2        Assembly requires z/Architecture
         SYSSTATE OSREL=ZOSV1R13   Assembly requires z/OS 1.13 & higher

EDITSUB  #START ,                  Start Procedure                     *
               AMODE=31,           Execute In 31-Bit Addressing Mode   *
               BASE=R12,           Define Base Register(s)             *
               COPY=YES,                                               *
               RMODE=ANY,          Execute In Any Residency Mode       *
               WKDSECT=SUBWRK,     Define WorkArea Name                *
               LOC=BELOW,          WorkArea Acquired Below The Line    *
               LEVEL=#V01R001      Maintenance Level           #V01R001

*-------
*        Check If Called From Within ISPF
*-------
         LINK  EP=ISPQRY
         LTR   R15,R15             Check If Within ISPF
         JZ    EDIT0100            B. If Yes
         MVC   WRKREC(L'NOTISPF),NOTISPF
         TPUT  WRKREC,L'NOTISPF    SAY: ISPF IS REQUIRED
         #SETRC 8                  Set Return Code
         J     EDITEXIT

*-------
*        Define ISPF Variables To Be Used
*-------
EDIT0100 DS    0H
         MVC   WRKFUNC,=CL8'LOAD'
         ISPLINK LOAD,SAVE=WRKLINK@,LIST=WRKPARMS,ERROR=ISPFERR

         MVC   WRKFUNC,=CL8'VDEFINE'
         ISPLINK VDEFINE,'WRKLAST ',WRKLAST,'FIXED ',L'WRKLAST,        *
               ERROR=ISPFERR
         ISPLINK VDEFINE,'WRKLRECL',WRKLRECL,'FIXED',L'WRKLRECL,       *
               ERROR=ISPFERR
         ISPLINK VDEFINE,'WRKREC  ',WRKREC,'CHAR ',L'WRKREC,           *
               ERROR=ISPFERR
         ISPLINK VDEFINE,'ZEDSMSG ',ZEDSMSG,'CHAR ',L'ZEDSMSG,         *
               ERROR=ISPFERR
         ISPLINK VDEFINE,'ZEDLMSG ',ZEDLMSG,'CHAR ',L'ZEDLMSG,         *
               ERROR=ISPFERR

*-------
*        Start EDIT MACRO & Obtain Data Set Information
*-------
         MVC   WRKFUNC,=CL8'ISREDIT'
         OI    WRKFLAG,WRK$MAC     Indicate "EDIT MACRO" Statement
         ISPLINK ISREDIT,0,MACRO,ERROR=ISPFERR                         *
                                   Start "EDIT MACRO" Processing
         NI    WRKFLAG,255-WRK$MAC
         ISPLINK ISREDIT,0,MACLREC,ERROR=ISPFERR                       *
                                   Save Logical Record Length
         ISPLINK ISREDIT,0,MACLAST,ERROR=ISPFERR                       *
                                   Save Last Line Number

*-------
*        Allocate & Open The Internal Reader
*-------
         ALLOC DDNTO=WRKDDN,SYSOUT=('A','INTRDR'),ERROR=S99FAIL

         MVC   WRKACB(MODACBL),MODACB
         MVC   WRKRPL(MODRPLL),MODRPL
         USING IFGACB,WRKACB
         USING IFGRPL,WRKRPL

         MVC   ACBDDNM,WRKDDN
         LA    R0,WRKACB
         ST    R0,RPLDACB
         LA    R0,WRKREC
         ST    R0,RPLAREA
         L     R0,WRKLRECL
         ST    R0,RPLRLEN

         MVC   WRKOPEN(MODOPENL),MODOPEN
         OPEN  (WRKACB,(OUTPUT)),MF=(E,WRKOPEN)
         LTR   R15,R15
         JZ    *+8
         EX    0,*                 ABEND S0C3 (Should Never Happen)

*-------
*        Read A Record At A Time & Submit To The Internal Reader
*-------
         XC    WRKLINE,WRKLINE     Initialize Line Counter
EDIT0200 DS    0H
         L     R1,WRKLINE
         AHI   R1,1                Increment Line Number
         ST    R1,WRKLINE
         CL    R1,WRKLAST          Check If Last Row Processed
         JH    EDIT0300            B. If Yes
         BAL   R14,ISRREAD         Read Next Row Of Data
         LTR   R15,R15             Check For An Error
         JNZ   EDITEXIT            B. If Yes

         PUT   RPL=WRKRPL          Output Current Record
         J     EDIT0200

*-------
*        Obtain the JES Job Id From The RPL & Close The Internal Reader
*-------
EDIT0300 DS    0H
         ENDREQ RPL=WRKRPL         Get JobNumber
         MVI   ZEDSMSG,C' '
         MVC   ZEDSMSG+1(L'ZEDSMSG-1),ZEDSMSG
         MVI   ZEDLMSG,C' '
         MVC   ZEDLMSG+1(L'ZEDLMSG-1),ZEDLMSG

         MVC   ZEDSMSG,=CL24'Job ________ Submitted'
         MVC   ZEDSMSG+4(8),RPLRBAR
         MVC   ZEDLMSG(8),RPLRBAR
         MVC   ZEDLMSG+9(47),=C'submitted 000000 records to the interna*
               l reader'
         MVC   ZEDLMSG+19(6),WRKCMD+17
         MVC   WRKFUNC,=CL8'SETMSG'
         ISPLINK SETMSG,'ISRZ001 ' Set ISPF Error Message

         MVC   WRKCLOSE(MODCLOSEL),MODCLOSE
         CLOSE (WRKACB),MF=(E,WRKCLOSE)

         MVC   WRKFUNC,=CL8'VDELETE'
         ISPLINK VDELETE,'*'       Delete Previously Defined Variables
         LA    R0,WRKDDN
         ST    R0,WRKDDN@          A(DDName)
         LHI   R0,L'WRKDDN
         STH   R0,WRKDDN@+4        L(DDName)
         FREE  DDN=WRKDDN@         Free The Internal Reader

*-------
*        Terminate
*-------
EDITEXIT #STOP ,                   Terminate

*-------
*        Obtain A Line From The EDIT Buffer
*-------
ISRREAD  DS    0H
         STM   R0,R14,WRKSAVE
         MVC   WRKCMD,MACREC       Copy "(WRKREC) = LINE"
         L     R1,WRKLINE
         CVD   R1,WRKDBL1          Convert Line Number To Decimal
         OI    WRKDBL1+7,X'0F'
         UNPK  WRKCMD+17(6),WRKDBL1

         MVI   WRKREC,C' '
         MVC   WRKREC+1(L'WRKREC-1),WRKREC

         MVC   WRKFUNC,=CL8'ISREDIT'
         ISPLINK ISREDIT,0,WRKCMD,ERROR=ISPFERR
*                                  Read Next Row
         LM    R0,R14,WRKSAVE
         BR    R14                 Return To Caller

*-------
*        Display Any Dynamic Allocation Error Messages
*-------
S99FAIL  DS    0H
         #SETRC (R15)              Set Return Code
         S99FAIL MF=(E,WRKFAIL)
         J     EDITEXIT

*-------
*        Display An ISPF Error
*-------
ISPFERR  DS    0H
         #SETRC (R15)              Set Return Code

         MVI   ZEDSMSG,C' '
         MVC   ZEDSMSG+1(L'ZEDSMSG-1),ZEDSMSG
         MVI   ZEDLMSG,C' '
         MVC   ZEDLMSG+1(L'ZEDLMSG-1),ZEDLMSG

         MVC   ZEDSMSG(8),WRKFUNC
         MVC   ZEDSMSG+9(6),=CL6'Failed'

         CLC   WRKFUNC,=CL8'ISREDIT'
         JNE   ISPFE100            B. If Not ISREDIT Request
         TM    WRKFLAG,WRK$MAC     Check For "EDIT MACRO" Statement
         JZ    ISPFE100            B. If Not
         MVC   ZEDSMSG(19),=C'''EDIT MACRO'' Failed'
         MVC   ZEDLMSG(28),=C'Improper Environment or Call'
         NI    WRKFLAG,255-WRK$MAC
         J     ISPFE200

ISPFE100 DS    0H
         MVC   ZEDLMSG(34),=C'Function Failed With A Return Code'
         CVD   R15,WRKDBL1         Convert Return Code To Decimal
         OI    WRKDBL1+7,X'0F'
         UNPK  ZEDLMSG+35(4),WRKDBL1

ISPFE200 DS    0H
         MVC   WRKFUNC,=CL8'SETMSG'
         ISPLINK SETMSG,'ISRZ001 '
         J     EDITEXIT

*-------
*        Edit MACRO Model Statements
*-------
MACRO    DC    C'¢MACRO¢                         '
MACLREC  DC    C'¢(WRKLRECL) = DATA_WIDTH¢       '
MACREC   DC    C'¢(WRKREC) = LINE 000000¢        '
MACLAST  DC    C'¢(WRKLAST) = LINENUM .ZLAST¢    '

*-------
*        Constants
*-------
         ISPLINK ,                 ISPLINK Keywords/Constants

NOTISPF  DC    C'This Program Requires An ISPF Environment'

MODACB   ACB   DDNAME=X,MACRF=(ADR,SEQ,OUT)
MODACBL  EQU   *-MODACB
MODRPL   RPL   ACB=*-*,OPTCD=(ADR,SEQ,SYN,NUP),                        *
               AREA=*-*,RECLEN=*-*,AREALEN=L'WRKREC
MODRPLL  EQU   *-MODRPL
MODOPEN  OPEN  (*-*,(OUTPUT)),MF=L
MODOPENL EQU   *-MODOPEN
MODCLOSE CLOSE (*-*),MF=L
MODCLOSEL EQU  *-MODCLOSE

*-------
*        Dynamic Work Area
*-------
         #STARTWA ,
         DYNSPACE ,                Dynamic Allocation Area
WRKACB   DS    0F,XL(MODACBL)      Internal Reader ACB
WRKCLOSE DS    0F,XL(MODCLOSEL)    Close Parameter List
WRKCMD   DS    CL32                Edit Macro Command Line Area
WRKDBL1  DS    D                   DoubleWord Work Area
WRKDDN   DS    CL8                 SYSOUT DDName
WRKDDN@  DS    A,AL2               DDName Address & Length
WRKFAIL  DS    8F                  S99FAIL Parameter List
WRKFLAG  DS    XL1                 Flag Byte
WRK$MAC  EQU   BIT0                - "ISREDIT MACRO" Statement
         DS    XL3                 Spare
WRKFUNC  DS    CL8                 Current ISPF Function
WRKLAST  DS    F                   Last Line Number
WRKLINE  DS    F                   Current Line Number
WRKLINK@ DS    A                   A(ISPLINK/ISREDIT)
WRKLRECL DS    F                   Current Data Set LRECL
WRKOPEN  DS    0F,XL(MODOPENL)     Open Parameter List
WRKPARMS DS    16F                 ISPLINK Parameter List
WRKREC   DS    CL256               Current ISPF EDIT Record
WRKRPL   DS    0F,XL(MODRPLL)      Internal Reader RPL
WRKSAVE  DS    16F                 Register Save Area

ZEDSMSG  DS    CL24                ISPF Short Message
ZEDLMSG  DS    CL80                ISPF Long Message
         #STOPWA ,

*-------
*        Required DSECTs
*-------
         #DSECTS ACB,ALLOC,RPL
         END   ,
