CATL     TITLE 'CATL - CATALOG LIST UTILITY'
*---------------------------------------------------------------------*
*                                                                     *
*        Support Email Address: zOS.JES2@Gmail.com                    *
*                                                                     *
*---------------------------------------------------------------------*

***********************************************************************
*                     *                                               *
*   C A T L           *            CATALOG LIST UTILITY               *
*                     *                                               *
***********************                                               *
*                                                                     *
*   FUNCTION :        LIST CATALOGED DATASETS AND OPTIONALLY LIST     *
*                     DCB | SPACE | VOLUME INFORMATION                *
*                                                                     *
*   SYNTAX:           CATL -                                          *
*                         LEVEL(QUALIFIER) -                 (OPTIONAL)
*                         DCB | SPACE | VOLUMES | HORIZONTAL (OPTIONAL)
*                                                                     *
*   REGISTER USAGE :   R0 - WORK REGISTER                             *
*                      R1 - WORK REGISTER                             *
*                      R2 - WORK REGISTER                             *
*                      R3 - WORK REGISTER                             *
*                      R4 - WORK REGISTER                             *
*                      R5 - WORK REGISTER                             *
*                      R6 - WORK REGISTER                             *
*                      R7 - WORK REGISTER                             *
*                      R8 - WORK REGISTER                             *
*                      R9 - WORK REGISTER                             *
*                     R10 - WORK REGISTER                             *
*                     R11 - >>---> PDL                                *
*                     R12 - BASE                                      *
*                     R13 - >>---> WORK AREA                          *
*                     R14 - RETURN ADDRESS                            *
*                     R15 - RETURN CODE                               *
*                                                                     *
*   MACROS : #CTGPL   #DSECTS  #INSTACK #IOSRL   #MODE    #START      *
*            #STARTWA #STOP    #STOPWA  CALLTSSR CAMLST   CATALOG     *
*            #EXEC    FREEMAIN GETMAIN  IECSDSL1 IKJEFFMT IKJENDP     *
*            IKJKEYWD          IKJNAME  IKJPARM  IKJPOSIT IKJSUBF     *
*            IKJTSMSG LINK     LOCATE   #MSG     OBTAIN   PUTLINE     *
*                                                                     *
*   ROUTINES CALLED : IEFEB4UV IKJPARS  IKJEFF02 IKJEFF19             *
*                                                                     *
*---------------------------------------------------------------------*
*                                                                     *
*   HISTORY : #DD84123 - MAY  2,1984 - USE FORMAT2 CATALOG WORKAREA   *
*             #DD99183 - JUL  2,1999 - CONVERT UNITNAME VIA IEFEB4UV  *
*             #DD00033 - FEB  2,2000 - SUPPORT PDSE/HFS DATASETS      *
*             #DD12062 - MAR  2,2012 - ADD EAV SUPPORT.               *
*                                                                     *
***********************************************************************

         ACONTROL OPTABLE(ZS3)     Assembly OPTCDE Table
         SPLEVEL  SET=6            Specify OS/390 R2 macro format
         SYSSTATE ARCHLVL=2        Assembly requires z/Architecture
         SYSSTATE OSREL=ZOSV1R13   Assembly requires z/OS 1.13 & higher

CATL     #START BASE=R12,WKCSECT=CATLWORK,                             *
               APARS=,COPY=YES,AMODE=24,                       #DD99183*
               REG1=R2,USING=(PSA,0),                                  *
               LEVEL=#V001R04                                  #DD12062

         USING CPPL,R2
         ST    R2,CATLCPPL         SAVE CPPL ADDRESS

*--------------------------------------------------------------------*
*        SET UP AN I/O SERVICE ROUTINE PARAMETER LIST                *
*--------------------------------------------------------------------*

         LA    R1,CATLIOPL         >>---> IOPL
         USING IOPL,R1
         MVC   IOPLUPT,CPPLUPT     INSERT ADDRESS OF UPT
         MVC   IOPLECT,CPPLECT     INSERT ADDRESS OF ECT
         MVC   IOPLECB,=A(CATLECB) INSERT ADDRESS OF ECB
         DROP  R1

         MVC   PUTLPARM(4),CPPLUPT   SAVE UPT ADDRESS
         MVC   PUTLPARM+4(4),CPPLECT SAVE ECT ADDRESS
         MVC   PUTLPARM+8(4),=A(CATLECB)

*--------------------------------------------------------------------*
*        SET UP IKJEFF02 (MESSAGE ROUTINE) PARAMETER LIST            *
*--------------------------------------------------------------------*

         LA    R0,MTCSECTP         >>---> MSG DESC SECTION
         ST    R0,MTPLPTR
         MVC   MTCPPLP,CATLCPPL    >>---> CPPL
         MVC   MTECBP,=A(CATLECB)  >>---> COMMUNICATION ECB
         OI    MTHIGH,B'10000000'  ON FOR STD LINKAGE
         MVC   MTCSECTP,=A(MSGCSECT)
         MVI   MTSW1,MTPUTLSW      PUTLINE MESSAGES
         LA    R0,INSERT           >>---> INSERT AREA
         STCM  R0,B'0111',MTADDR
         MVI   MTLEN,44            SET INSERT LENGTH
         MVC   INSERT,BLANKS

*--------------------------------------------------------------------*
*        SET UP A PARSE PARAMETER LIST (PPL)                         *
*--------------------------------------------------------------------*

         LA    R1,CATLPPL          >>---> PPL
         USING PPL,R1
         MVC   PPLUPT,CPPLUPT      INSERT ADDRESS OF UPT
         MVC   PPLECT,CPPLECT      INSERT ADDRESS OF ECT
         MVC   PPLCBUF,CPPLCBUF    INSERT ADDRESS OF CBUF
         MVC   PPLECB,=A(CATLECB)  INSERT ADDRESS OF ECB
         MVC   PPLANS,=A(CATLPDLP) INSERT ADDRESS OF ANSWER PLACE
         MVC   PPLPCL,=A(PCL)      INSERT ADDRESS OF PCL
         CALLTSSR EP=IKJPARS,MF=(E,(1))
         DROP  R1
         LTR   R15,R15             CHECK IF PARSE WAS SUCCESSFUL
         BNZ   CATLERR1            IF NOT PROCESS ERROR
         L     R11,CATLPDLP        >>---> PDL
         USING IKJPARMD,R11

*---------------------------------------------------------------------*
*        SETUP THE LEVEL ACCORDINGLY     (PREFIX/LEVEL./'LEVEL')      *
*---------------------------------------------------------------------*

         MVC   DSNBUF,BLANKS       CLEAR THE DSNAME FIELD
         CLI   LEVELKW+1,1         CHECK IF LEVEL(?) WAS SPECIFIED
         BE    CATL0010
         L     R1,CPPLUPT          >>---> UPT
         USING UPT,R1
         MVC   DSNBUF+1(7),UPTPREFX
         SLR   R9,R9               CLEAR FOR INSERT
         ICM   R9,B'0001',UPTPREFL INSERT PREFIX LENGTH
         BZ    CATLERR2
         B     CATL0020
         DROP  R1
CATL0010 DS    0H
         L     R1,DSNPDE           >>---> DSNAME
         LH    R9,DSNPDE+4         LOAD DSNAME LENGTH
         #EXEC -R9,MVC,DSNBUF+1(*-*),0(R1)
         LA    R9,1(,R9)           RESTORE LENGTH OF DSNAME
         TM    DSNPDE+6,X'40'      CHECK IF LEVEL WAS ENTERED IN QUOTES
         BO    CATL0030
CATL0020 DS    0H
         LA    R1,DSNBUF+1(R9)     >>---> END OF LEVEL
         MVI   0(R1),C'.'          INSERT A PERIOD
         LA    R9,1(,R9)           INCREMENT LENGTH OF DSNAME
CATL0030 DS    0H
         STC   R9,DSNBUF           SAVE LENGTH OF LEVEL

*---------------------------------------------------------------------*
*        LOCATE ALL CATALOG ENTRIES FOR THIS PREFIX                   *
*---------------------------------------------------------------------*

         GETMAIN VU,LA=WORKSIZE,A=WORKADDR
CATL0040 DS    0H
         LM    R2,R3,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         XC    @CTGPL,@CTGPL
         #CTGPL LISTCAT,           SETUP CATALOG PARAMETER LIST        *
               CRI=DSNBUF,                                             *
               WORK=((R2),(R3),FORMAT2),                       #DD84123*
               OPTIONS=(NAME,GENLD,RCATN,SUPLT,AM0),                   *
               MF=(E,@CTGPL)
         CATALOG @CTGPL
         LTR   R15,R15             CHECK CATALOG RETURN CODE
         BNZ   CATLERR3            CHECK FOR RECOVERABLE ERROR

*---------------------------------------------------------------------*
*        CALCULATE THE NUMBER OF ENTRIES & PRINT THE TITLES           *
*---------------------------------------------------------------------*

         CLC   4(4,R2),=X'00000008'
         BNE   CATL0050
         XC    8(2,R2),8(R2)       CLEAR TOP HALF OF CVOL LENGTH
         LA    R2,4(,R2)           >>---> CVOL WORK AREA
CATL0050 DS    0H
         L     R7,4(,R2)           LOAD AMOUNT USED IN WORKAREA
         SLR   R6,R6               PREPARE FOR DIVIDE
         LA    R5,8(,R2)           >>---> CATALOG NAME
         MVC   INSERT,1(R5)        COPY CATALOG NAME TO INSERT AREA
         D     R6,=F'45'           CALCULATE THE NUMBER OF DSNAMES
         BCTR  R7,R0               SUBTRACT 1 FOR CATALOG NAME
         ST    R7,ENTRIES          SAVE THE NUMBER OF ENTRIES
         LTR   R7,R7               CHECK ENTRY COUNT
         BNP   CATLER3C
         CLI   KEYWORD1+1,4        CHECK FOR "HORIZONTAL"
         BE    CATL0070
         MVC   MTMSGID,=C'0004'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         MVC   PRINTLNE+4(44),MESSAGE1
         MVC   PRINTLNE+49(31),MESSAGE2
         CLI   KEYWORD1+1,1        CHECK FOR "DCB"
         BE    CATL0060
         MVC   PRINTLNE+49(31),MESSAGE3
         CLI   KEYWORD1+1,2        CHECK FOR "SPACE"
         BE    CATL0060
         MVC   PRINTLNE+49(31),MESSAGE4
         CLI   KEYWORD1+1,3        CHECK FOR "VOLUMES"
         BNE   CATL0120
CATL0060 DS    0H
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0120            SKIP SIDEWAYS PRINTING

*---------------------------------------------------------------------*
*        PRODUCE A "HORIZONTAL" CATALOG LIST                          *
*---------------------------------------------------------------------*

CATL0070 DS    0H
         LA    R5,45(,R5)          >>---> PAST CATALOG NAME
CATL0080 DS    0H
         MVC   PRINTLNE+4(79),BLANKS
         LA    R8,PRINTLNE+4       >>---> START OF MESSAGE AREA
         LA    R10,PRINTLNE+4+79   >>---> END   OF MESSAGE AREA
CATL0090 DS    0H
         CLI   0(R5),C'0'          CHECK FOR CATALOG NAME ENTRY
         BE    CATL0110            B. IF YES
         LA    R1,45(R5)           >>---> END OF THE DSNAME
         TRT   1(44,R5),TRTABLE    FIND THE END OF THE DSNAME
         SLR   R2,R2               CLEAR FOR INSERT
         IC    R2,DSNBUF           LOAD PREFIX LENGTH
         LA    R3,1(R2,R5)         >>---> PAST DATASET NAME PREFIX
         SR    R1,R3               CALCULATE LENGTH OF THE DSNAME
         LA    R2,0(R1,R8)         >>---> WHERE DSNAME WILL END
         CR    R2,R10              CHECK IF IT WILL OVERFLOW
         BL    CATL0100
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0080            INSERT THE CURRENT DSNAME
CATL0100 DS    0H
         #EXEC -R1,MVC,0(*-*,R8),0(R3)
         LA    R8,2(R1,R8)         >>---> NEXT DSN BUFFER
CATL0110 DS    0H
         LA    R5,45(,R5)          >>---> NEXT DATASET NAME
         BCT   R7,CATL0090
         LA    R1,PRINTLNE+4       >>---> START OF PRINT LINE
         CR    R1,R8               CHECK IF ANYTHING IN THE LINE
         BE    CATL0420
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
         B     CATL0420

*---------------------------------------------------------------------*
*        FORMAT & PRINT THE INDIVIDUAL DATASET LINES                  *
*---------------------------------------------------------------------*

CATL0120 DS    0H
         LA    R5,45(,R5)          >>---> NEXT DATASET NAME
         MVC   PRINTLNE+4(44),1(R5)
         MVC   PRINTLNE+49(31),BLANKS
         MVC   DSNAME,1(R5)        SAVE THE DATASET NAME
         CLI   0(R5),C'0'          CHECK FOR CATALOG NAME ENTRY
         BNE   CATL0130            B. IF NOT
         L     R1,ENTRIES
         BCTR  R1,0                SUBTRACT 1 FROM ENTRY COUNT
         ST    R1,ENTRIES
         B     CATL0410
CATL0130 DS    0H
         CLI   KEYWORD2+1,1        CHECK FOR "NOPREFIX"
         BNE   CATL0140
         MVC   PRINTLNE+4(44),BLANKS
         LA    R1,43               LOAD MAXIMUM DATASET LENGTH
         SLR   R2,R2               CLEAR FOR INSERT
         IC    R2,DSNBUF           LOAD PREFIX LENGTH
         LA    R3,DSNAME           >>---> DATASET NAME
         LA    R3,0(R2,R3)         >>---> PAST DATASET NAME PREFIX
         SR    R1,R2               CALCULATE REMAINING LENGTH
         #EXEC R1,MVC,PRINTLNE+4(*-*),0(R3)

*---------------------------------------------------------------------*
*        FORMAT THE "DCB" LINE                                        *
*---------------------------------------------------------------------*

CATL0140 DS    0H
         CLI   KEYWORD1+1,1        CHECK FOR "DCB"
         BNE   CATL0270
         CLI   0(R5),CTGTGBS       CHECK FOR "GDG BASE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTCL        CHECK FOR "CLUSTER"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTMCAT      CHECK FOR "MASTER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTPGSP      CHECK FOR "PAGE SPACE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTUCAT      CHECK FOR "USER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         LOCATE NAMEPL             GET CATALOG INFORMATION
         LTR   R15,R15             CHECK LOCATE RETURN CODE
         BNZ   CATL0400
         MVC   VOLSER,WORKAREA+6   SAVE VOLSER
         TM    WORKAREA+4,UCB3DACC CHECK FOR DIRECT ACCESS DEVICES
         BO    CATL0150
         MVC   MESSAGE9(6),VOLSER
         MVC   PRINTLNE+49(31),MESSAGE9
         B     CATL0400
CATL0150 DS    0H
         OBTAIN SEARCHPL,EADSCB=OK GET THE VTOC INFORMATION    #DD12062
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0160
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0160 DS    0H
         MVC   PRINTLNE+50(3),=C'POE'                          #DD00033
         TM    DS1SMSFG,DS1PDSE    CHECK IF DSN IS "PDSE"      #DD00033
         BO    CATL0180                                        #DD00033
         MVC   PRINTLNE+50(3),=C'HFS'                          #DD00033
         TM    DS1SMSFG,DS1PDSEX   CHECK IF DSN IS "HFS"       #DD00033
         BO    CATL0180                                        #DD00033
         MVC   PRINTLNE+50(3),=C'PS '
         TM    DS1DSORG,DS1DSGPS   CHECK IF DSN IS "PS"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'PO '
         TM    DS1DSORG,DS1DSGPO   CHECK IF DSN IS "PO"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'DA '
         TM    DS1DSORG,DS1DSGDA   CHECK IF DSN IS "DA"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'IS '
         TM    DS1DSORG,DS1DSGIS   CHECK IF DSN IS "IS"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'VS '
         TM    DS1DSORG+1,DS1ORGAM CHECK IF DSN IS "AM"
         BO    CATL0170
         MVC   PRINTLNE+50(3),=C'?? '
CATL0170 DS    0H
         TM    DS1DSORG,DS1DSGU    CHECK FOR UNMOVABLE DSORG
         BNO   CATL0180
         MVI   PRINTLNE+52,C'U'
CATL0180 DS    0H
         LA    R3,PRINTLNE+55      >>---> RECORD FORMAT SPACE
         TM    DS1RECFM,X'C0'      CHECK FOR RECFM = U
         BNO   CATL0190
         MVI   0(R3),C'U'
         LA    R3,1(,R3)           >>---> PAST "U"
         B     CATL0220            CHECK FOR TRACK OVERFLOW
CATL0190 DS    0H
         TM    DS1RECFM,X'80'      CHECK FOR RECFM = F
         BNO   CATL0200
         MVI   0(R3),C'F'
         LA    R3,1(,R3)           >>---> PAST "F"
         B     CATL0210            CHECK FOR BLOCKING
CATL0200 DS    0H
         TM    DS1RECFM,X'40'      CHECK FOR RECFM = V
         BNO   CATL0210
         MVI   0(R3),C'V'
         LA    R3,1(,R3)           >>---> PAST "V"
CATL0210 DS    0H
         TM    DS1RECFM,X'10'      CHECK IF DATASET IS BLOCKED
         BNO   CATL0220
         MVI   0(R3),C'B'
         LA    R3,1(,R3)           >>---> PAST "B"
CATL0220 DS    0H
         TM    DS1RECFM,X'20'      CHECK FOR TRACK OVERFLOW
         BNO   CATL0230
         MVI   0(R3),C'T'
         LA    R3,1(,R3)           >>---> PAST "T"
CATL0230 DS    0H
         TM    DS1RECFM,X'08'      CHECK FOR SPANNED OR STANDARD
         BNO   CATL0240
         MVI   0(R3),C'S'
         LA    R3,1(,R3)           >>---> PAST "S"
CATL0240 DS    0H
         TM    DS1RECFM,X'04'      CHECK FOR ASA CNTL CHARS
         BNO   CATL0250
         MVI   0(R3),C'A'
         LA    R3,1(,R3)           >>---> PAST "A"
CATL0250 DS    0H
         TM    DS1RECFM,X'02'      CHECK FOR MACHINE CNTL CHARS
         BNO   CATL0260
         MVI   0(R3),C'M'
CATL0260 DS    0H
         SLR   R0,R0
         ICM   R0,B'0011',DS1LRECL LOAD RECORD LENGTH
         CVD   R0,DBLWORD
         MVC   PRINTLNE+60(6),=X'402020202120'
         ED    PRINTLNE+60(6),DBLWORD+5
         SLR   R0,R0
         ICM   R0,B'0011',DS1BLKL  LOAD BLOCK SIZE
         CVD   R0,DBLWORD
         MVC   PRINTLNE+67(6),=X'402020202120'
         ED    PRINTLNE+67(6),DBLWORD+5
         MVC   FULLWORD(3),DS1EXPDT    SAVE THE EXPIRY DATE
         L     R0,FULLWORD             LOAD THE EXPIRY DATE
         SRL   R0,24                   SHIFT TO GET THE YEAR
         CVD   R0,DBLWORD
         UNPK  PRINTLNE+75(2),DBLWORD
         OI    PRINTLNE+76,X'F0'   CONVERT SIGN BIT
         L     R0,FULLWORD             LOAD THE EXPIRY DATE
         SLL   R0,8                    SHIFT OFF THE YEAR
         SRL   R0,16                   SHIFT BACK FOR DATE
         CVD   R0,DBLWORD
         UNPK  PRINTLNE+77(3),DBLWORD
         OI    PRINTLNE+79,X'F0'   CONVERT SIGN BIT
         B     CATL0400

*---------------------------------------------------------------------*
*        FORMAT THE "SPACE" LINE                                      *
*---------------------------------------------------------------------*

CATL0270 DS    0H
         CLI   KEYWORD1+1,2        CHECK FOR "SPACE"
         BNE   CATL0390
         CLI   0(R5),CTGTGBS       CHECK FOR "GDG BASE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTCL        CHECK FOR "CLUSTER"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTMCAT      CHECK FOR "MASTER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTPGSP      CHECK FOR "PAGE SPACE"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         CLI   0(R5),CTGTUCAT      CHECK FOR "USER CATALOG"
         BE    CATL0400            - NO ACTUAL DATASET EXISTS
         LOCATE NAMEPL             GET CATALOG INFORMATION
         LTR   R15,R15             CHECK RETURN CODE
         BNZ   CATL0400
         MVC   VOLSER,WORKAREA+6   SAVE VOLSER
         TM    WORKAREA+4,UCB3DACC CHECK FOR DIRECT ACCESS DEVICES
         BO    CATL0280
         MVC   MESSAGE9(6),VOLSER
         MVC   PRINTLNE+49(31),MESSAGE9
         B     CATL0400
CATL0280 DS    0H
         OBTAIN VTOCPL,EADSCB=OK   GET VTOC INFORMATION        #DD12062
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0290
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0290 DS    0H
         MVC   TRKCYL,DS4DEVSZ+2   SAVE # TRACKS PER CYL
         OBTAIN SEARCHPL,EADSCB=OK GET FORMAT 1 DSCB           #DD12062
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0300
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0300 DS    0H
         SLR   R3,R3
         IC    R3,DS1NOEPV         LOAD THE NUMBER OF EXTENTS
         CVD   R3,DBLWORD
         MVC   PRINTLNE+62(4),=X'40202120'
         ED    PRINTLNE+62(4),DBLWORD+6
         MVC   PRINTLNE+74(4),=C'CYLS'
         TM    DS1SCALO,X'C0'          CHECK FOR CYLINDER ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'BLKS'
         TM    DS1SCALO,X'40'          CHECK FOR BLOCK ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'TRKS'
         TM    DS1SCALO,X'80'          CHECK FOR TRACK ALLOCATION
         BO    CATL0310
         MVC   PRINTLNE+74(4),=C'ABST'
CATL0310 DS    0H
         SLR   R0,R0
         ICM   R0,B'0111',DS1SCALO+1   LOAD SECONDARY ALLOCATION
         CVD   R0,DBLWORD
         MVC   PRINTLNE+67(6),=X'402020202120'
         ED    PRINTLNE+67(6),DBLWORD+5
         NI    DS1LSTAR,X'7F'      TURN OFF HIGH ORDER BIT
         SLR   R3,R3
         ICM   R3,B'0011',DS1LSTAR LOAD USED TRACKS
         CLC   DS1LSTAR,=X'000000' CHECK IF ANY ARE USED
         BE    CATL0320
         LA    R3,1(,R3)
CATL0320 DS    0H
         CVD   R3,DBLWORD
         MVC   PRINTLNE+54(6),=X'402020202120'
         ED    PRINTLNE+54(6),DBLWORD+5
         XC    FULLWORD,FULLWORD   CLEAR ALLOCATED TRK COUNTER
         LA    R1,DS1EXT1          >>---> FIRST EXTENT
         LA    R2,3                LOAD LOOP FACTOR
CATL0330 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0330         LOOP 4 TIMES
         CLC   DS1PTRDS,=X'0000000000' CHECK FOR A FMT2 OR FMT3 DSCB
         BE    CATL0380
         MVC   CCHHR,DS1PTRDS      SAVE DSCB POINTER
CATL0340 DS    0H
         OBTAIN SEEKPL,EADSCB=OK                               #DD12062
         LTR   R15,R15             CHECK RETURN CODE
         BZ    CATL0350
         BAL   R14,OBTAINFO        FORMAT OBTAIN ERROR MESSAGE
         B     CATL0400
CATL0350 DS    0H
         CLI   DS3FMTID,C'3'       CHECK FOR FORMAT 3 DSCB
         BNE   CATL0340
         LA    R1,DS3EXTNT         >>---> FOUR EXTENTS
         LA    R2,4                LOAD LOOP FACTOR
CATL0360 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0360         LOOP 4 TIMES
         LA    R1,DS3ADEXT         >>---> NINE EXTENTS
         LA    R2,9                LOAD LOOP FACTOR
CATL0370 DS    0H
         BAL   R14,CONVCCHH        CONVERT + TOTAL TRACKS
         LA    R1,10(,R1)          >>---> NEXT EXTENT
         BCT   R2,CATL0370         LOOP 9 TIMES
         MVC   CCHHR,DS3PTRDS      SAVE DSCB POINTER
         CLC   CCHHR,=X'0000000000' CHECK FOR MORE DSCBS
         BNE   CATL0340
CATL0380 DS    0H
         L     R0,FULLWORD         LOAD ALLOCATED TRACK COUNT
         CVD   R0,DBLWORD
         MVC   PRINTLNE+48(6),=X'402020202120'
         ED    PRINTLNE+48(6),DBLWORD+5
         B     CATL0400

*---------------------------------------------------------------------*
*        FORMAT THE "VOLUMES" LINE                                    *
*---------------------------------------------------------------------*

CATL0390 DS    0H
         CLI   KEYWORD1+1,3        CHECK FOR "VOLUMES"
         BNE   CATL0400
         LOCATE NAMEPL             GET THE CATALOG INFORMATION
         LTR   R15,R15             CHECK LOCATE RETURN CODE
         BNZ   CATL0400
         MVC   PRINTLNE+49(6),WORKAREA+6
         NI    WORKAREA+3,X'DF'    TURN OFF SHARED BIT IN THE DEVTYPE
         MVC   UVDEVT,WORKAREA+2   COPY DEVICE TYPE            #DD99183
         NI    UVDEVT+1,X'DF'      TURN OFF "SHARED" BIT       #DD99183
         LA    R0,UVUNTAB                                      #DD99183
         ST    R0,UVUNTAB@         SAVE UNIT TABLE ADDRESS     #DD99183
         LA    R0,UVFLAGS                                      #DD99183
         ST    R0,UVFLAGS@         SAVE FLAGS ADDRESS          #DD99183
         OI    UVFLAGS@,X'80'      INDICATE END OF LIST        #DD99183
         MVC   UVFLAGS,=X'2100'    INSERT REQUEST FLAGS        #DD99183
         LA    R1,UVPLIST                                      #DD99183
         LINK  EP=IEFEB4UV         CALL SERVICE ROUTINE        #DD99183
         MVC   PRINTLNE+57(8),=CL8'????????'                   #DD99183
         LTR   R15,R15             CHECK IF UNITNAME RETURNED  #DD99183
         BNZ   CATL0400            B. IF NOT                   #DD99183
         MVC   PRINTLNE+57(8),UVUNAME                          #DD99183

CATL0400 DS    0H
         LM    R2,R4,PUTLPARM      LOAD UPT/ECT/ECB ADDRESS
         PUTLINE PARM=PUTLINEL,UPT=(R2),ECT=(R3),ECB=(R4),             *
               OUTPUT=(PRINTLNE,TERM,SINGLE,DATA),                     *
               MF=(E,IOPLADS)
CATL0410 DS    0H
         BCT   R7,CATL0120         PROCESS ALL ENTRIES IN THE BUFFER

*---------------------------------------------------------------------*
*        PRINT THE FOOTING TITLES                                     *
*---------------------------------------------------------------------*

         MVC   INSERT,BLANKS
         MVC   INSERT(4),ENTRIES   COPY # OF DATASETS
         OI    MTLEN,X'80'         SET HEX CONVERSION FLAG
         MVC   MTMSGID,=C'0005'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)

*---------------------------------------------------------------------*
*        FREE THE CATALOG WORK AREA IF ONE WAS OBTAINED               *
*---------------------------------------------------------------------*

CATL0420 DS    0H
         LM    R3,R4,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         LTR   R3,R3               CHECK IF A WORKAREA EXISTS
         BZ    CATLEXIT
         FREEMAIN RU,LV=(R4),A=(R3)
CATLEXIT #STOP ,                   RETURN TO CALLER

*---------------------------------------------------------------------*
*        EXTENT SIZE CALCULATION SUBROUTINE                           *
*---------------------------------------------------------------------*

CONVCCHH DS    0H
         CLC   2(4,R1),=F'0'       IS THERE A START CCHHR
         BER   R14                 IF NOT RETURN TO CALLER
         LH    R3,2(R1)            LOAD START CYL ADDRESS
         MH    R3,TRKCYL           CONVERT TO TRKS
         AH    R3,4(R1)            ADD ADDITIONAL TRKS
         LH    R4,6(R1)            LOAD END CYL ADDRESS
         MH    R4,TRKCYL           CONVERT TO TRKS
         AH    R4,8(R1)            ADD ADDITIONAL TRKS
         LA    R4,1(,R4)           ADD 1 FOR RELATIVE TRACK 0
         SR    R4,R3               CALCULATE EXTENT SIZE
         A     R4,FULLWORD         ADD ALREADY ACCUMULATED
         ST    R4,FULLWORD         SAVE NEW TOTAL
         BR    R14                 RETURN TO CALLER

*---------------------------------------------------------------------*
*        OBTAIN ERROR MESSAGE FORMATING SUBROUTINE                    *
*---------------------------------------------------------------------*

OBTAINFO DS    0F
         MVC   MESSAGE5(6),VOLSER
         MVC   MESSAGE6+23(6),VOLSER
         MVC   MESSAGE7+18(6),VOLSER
         MVC   PRINTLNE+49(31),BLANKS
         LA    R1,OBTABLE          >>---> MESSAGE TABLE
         LA    R1,0(R15,R1)        >>---> SPECIFIC MESSAGE POINTER
         ICM   R1,B'1111',0(R1)    >>---> SPECIFIC MESSAGE
         BZR   R14                 IF NO MESSAGE RETURN TO CALLER
         MVC   PRINTLNE+49(31),0(R1)
         BR    R14                 RETURN TO CALLER

*---------------------------------------------------------------------*
*        IKJPARSE ERROR PROCESSING                                    *
*---------------------------------------------------------------------*

CATLERR1 DS    0H
         LA    R1,JEFF19PL         >>---> PARAMETER LIST
         LA    R0,4(,R1)           >>---> PARAMETERS
         ST    R0,0(R1)            SAVE IN PARAMETER LIST
         XC    4(44,R1),4(R1)      CLEAR REMAINDER OF PARAMETER LIST
         ST    R15,8(R1)           SAVE RETURN CODE
         MVI   17(R1),21           SET CALLER ID (PARSE ERROR)
         MVC   20(4,R1),CATLCPPL   INSERT CPPL ADDRESS
         LA    R0,CATLECB          >>---> ECB
         ST    R0,24(R1)           SAVE ECB ADDRESS
         LINK  EP=IKJEFF19         ISSUE PARSE ERROR MESSAGE
         B     CATL0420

*---------------------------------------------------------------------*
*        NULL PREFIX ERROR PROCESSING                                 *
*---------------------------------------------------------------------*

CATLERR2 DS    0H
         MVC   MTMSGID,=C'0002'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420

*---------------------------------------------------------------------*
*        CATALOG ERROR PROCESSING                                     *
*---------------------------------------------------------------------*

CATLERR3 DS    0H
         C     R15,=A(RCVLSZ)      CHECK IF "WORKAREA" IS TOO SMALL
         BNE   CATLER3B
         LM    R3,R4,WORKADDR      LOAD WORKAREA ADDRESS & SIZE
         L     R2,4(,R3)           LOAD REQUIRED WORK SIZE
         FREEMAIN RU,LV=(R4),A=(R3)
         GETMAIN  RU,LV=(R2)       OBTAIN REQUIRED "WORKAREA" STORAGE
         STM   R1,R2,WORKADDR      SAVE NEW "WORKAREA" ADDRESS & SIZE
         B     CATL0040            BRANCH BACK TO REISSUE LISTCAT
CATLER3B DS    0H
         C     R15,=A(RCENT)       CHECK IF NO ENTRIES FOUND
         BNE   CATLER3D
CATLER3C DS    0H
         MVC   MTMSGID,=C'0006'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420
CATLER3D DS    0H
         ST    R15,INSERT
         OI    MTLEN,X'80'         SET HEX CONVERSION FLAG
         MVC   MTMSGID,=C'0003'    INSERT MESSAGE ID
         CALLTSSR EP=IKJEFF02,     CALL MESSAGE ROUTER                 *
               MF=(E,MTPARML)
         B     CATL0420

*---------------------------------------------------------------------*
*        WORK AREA                                                    *
*---------------------------------------------------------------------*

         #STARTWA ,
         IKJEFFMT MTDSECT=NO,MTNINST=2
@CTGPL   DS    CL(CTGPLLEN)
BLANKS   DC    CL80' '
CATLCPPL DS    F                   @ CMD PROCESSOR PARM LIST
CATLECB  DS    F                   ECB
CATLIOPL DS    4F                  I/O PARAM LIST
CATLPDLP DC    F'0'                PDL POINTER
CATLPPL  DS    7F                  PARSE PARAMETER LIST
CCHHR    DS    XL5                     CCHHR
DBLWORD  DS    D
DSNBUF   DS    CL45                DSNAME SAVE AREA
DSNAME   DS    CL44                DATASET NAME
ENTRIES  DS    F
FMT4DSN  DC    44X'04'             FORMAT4.DSCB NAME
FULLWORD DS    F
INSERT   DS    CL44                MESSAGE INSERT # 1
IOPLADS  DS    4F
JEFF19PL DS    12F                 DAIRFAIL PARAMETER LIST
MESSAGE1 DC    CL44'- DATASET NAME -'
MESSAGE2 DC    CL31'DSORG-RECFM-LRECL-BLKSIZE-EXPDT'
MESSAGE3 DC    CL31'ALLOC--USED-EXTENTS-SECONDARY'
MESSAGE4 DC    CL31'VOLSER DEVTYPE'
MESSAGE5 DC    CL31'XXXXXX NOT MOUNTED'
MESSAGE6 DC    CL31'FMT1 DSCB NOT FOUND ON XXXXXX'
MESSAGE7 DC    CL31'I/O ERROR READING XXXXXX VTOC'
MESSAGE8 DC    CL31'INVALID DSCB CHAIN POINTER'
MESSAGE9 DC    CL31'XXXXXX IS NOT A DASD VOLUME'
NAMEPL   CAMLST NAME,DSNAME,,WORKAREA

UVPLIST  DS    0F                                              #DD99183
UVUNTAB@ DS    A                   A(UNIT TABLE)               #DD99183
UVFLAGS@ DS    A                   A(FLAGS)                    #DD99183
UVUNTAB  DS    0XL16               UNIT TABLE                  #DD99183
UVUNAME  DS    CL8                 RETURNED UNIT NAME          #DD99183
UVDEVT   DS    XL4                 DEVICE TYPE                 #DD99183
UVATTR@  DS    A                   A(ATTRIBUTE AREA)           #DD99183
UVFLAGS  DS    XL2                 FLAGS                       #DD99183

OBTABLE  DC    A(0)
         DC    A(MESSAGE5)
         DC    A(MESSAGE6)
         DC    A(MESSAGE7)
         DC    A(0)
         DC    A(MESSAGE8)
PRINTLNE #MSG  '                                                       *
                                       ' - 4 BYTE HEADER & 79 BYTE MSG
PUTLINEL PUTLINE MF=L
PUTLPARM DS    3F                  >>---> UPT/ECT/ECB
SEARCHPL CAMLST SEARCH,DSNAME,VOLSER,WORKAREA,EADSCB=OK        #DD12062
SEEKPL   CAMLST SEEK,CCHHR,VOLSER,WORKAREA,EADSCB=OK           #DD12062
TRKCYL   DS    H                   TRACKS PER CYLINDER
TRTABLE  DC    XL256'00'           TRANSLATE TABLE
         ORG   TRTABLE+C' '
         DC    X'FF'               FIND A BLANK
         ORG   ,
VOLSER   DS    CL6
VTOCPL   CAMLST SEARCH,FMT4DSN,VOLSER,WORKAREA,EADSCB=OK       #DD12062
WORKADDR DS    D                   RETURNED TABLE ADDRESS & SIZE
WORKAREA DS    0F,265C             LOCATE/OBTAIN WORKAREA
         PRINT NOGEN
         ORG   WORKAREA-L'DS1DSNAM
         IECSDSL1 1                FORMAT 1 DSCB
         ORG   WORKAREA
         IECSDSL1 3                FORMAT 3 DSCB
         ORG   WORKAREA
         IECSDSL1 4                FORMAT 4 DSCB
         ORG   ,
         PRINT GEN
WORKSIZE DC    F'53'               MINIMUM TABLE SIZE (45*1+8)
         DC    F'45008'            MAXIMUM TABLE SIZE (45*100+8)
         #STOPWA ,

*---------------------------------------------------------------------*
*        COMMAND PARAMETERS                                           *
*---------------------------------------------------------------------*

         PRINT NOGEN

PCL      IKJPARM
LEVELKW  IKJKEYWD
         IKJNAME 'L',SUBFLD=LEVELSUB,ALIAS='LEVEL'
KEYWORD1 IKJKEYWD
         IKJNAME 'DCB',                                                *
               ALIAS=('DSORG','RECFM','LRECL','BLKSIZE','EXPDT')
         IKJNAME 'SPACE',ALIAS='TRACKS'
         IKJNAME 'VOLUMES',ALIAS='UNIT'
         IKJNAME 'HORIZONTAL',ALIAS='SIDEWAYS'
KEYWORD2 IKJKEYWD
         IKJNAME 'NOPREFIX'
LEVELSUB IKJSUBF
DSNPDE   IKJPOSIT DSNAME,PROMPT='DATA SET NAME HIGH LEVEL QUALIFIER'
         IKJENDP

*---------------------------------------------------------------------*
*        MESSAGES                                                     *
*---------------------------------------------------------------------*

MSGCSECT CSECT ,
         IKJTSMSG ('CATL001I Unable to list catalog+'),0001
         IKJTSMSG ('CATL002I No prefix in User Profile Table'),0002,   *
               0001
         IKJTSMSG ('CATL003I Superlocate failed.  Return code=',,),0003
         IKJTSMSG ('CATL004I Entries from catalog ',,),0004
         IKJTSMSG ('CATL005I Number of entries listed = ',,),0005
         IKJTSMSG ('CATL006I No entries found'),0006
         IKJTSMSG

*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*

         #DSECTS CATLG,CVT,DCB,EDT,JESCT,JFCB,TCB,TIOT,TSO,UCB,PSA
         END   ,
