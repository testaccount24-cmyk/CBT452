SYSMSGS  TITLE '- SYSMSGS  - Sample Code To Write To SYSMSGS'
*---------------------------------------------------------------------*
*                                                                     *
*        Support Email Address: zOS.JES2@Gmail.com                    *
*                                                                     *
*---------------------------------------------------------------------*

         ACONTROL OPTABLE(ZS3)     Assembly OPTCDE Table
         SPLEVEL  SET=6            Specify OS/390 R2 MACRO Format
         SYSSTATE ARCHLVL=2        Assembly Requires Z/Architecture
         SYSSTATE OSREL=ZOSV1R13   Assembly Requires Z/OS 1.13 & Higher

*
* NOTE: The BASE Must NOT Be R12 As It's Used By IEFTB723
*

SYSMSGS  #START ,                  Start Procedure                     *
               AMODE=CAP31,        Execute In 31-Bit AMode             *
               RMODE=ANY,          Execute In Any RMode                *
               BASE=(R11),         Define Base Register(s)             *
               REG0=R0,            Save Input Parameter Address        *
               REG1=R1,            Save Input Parameter Address        *
               WKDSECT=SAMPWRK,    Define WorkArea Name                *
               LOC=BELOW,          Indicate 24-Bit WorkArea            *
               LEVEL=**TEST**      Maintenance Level

*---------------------------------------------------------------------*
*                                                                     *
*        Call IEFTB723 To Write To SYSMSGS                            *
*                                                                     *
*---------------------------------------------------------------------*

         MODESET KEY=ZERO,MODE=SUP

         LOAD  EP=IEFTB723         Load Write-To-Programmer Routine
         ST    R0,@TB723

         USING PSA,0
         L     R1,PSATOLD          A(TCB)
         USING TCB,R1
         ICM   R1,7,TCBTCT+1       A(TCT)
         USING SMFTCT,R1
         SLR   R2,R2
         ICM   R2,7,TCTLCTAD+1     A(LCT)
         LA    R12,0(,R2)
         USING LCT,R12
         LA    R1,TESTMSG+2        A(Message Text)
         ST    R1,LCTPARM1         Set Address In LCT
         LH    R15,TESTMSG         L(Message Text)
         STH   R15,LCTPARM2+2      Set Length In LCT

         L     R15,@TB723          Call IEFTB723 To
         BASSM R14,R15             . Queue Message To SYSMSGS

         DELETE EP=IEFTB723        Remove Write-To-Programmer Routine

         MODESET KEY=NZERO,MODE=PROB

*---------------------------------------------------------------------*
*                                                                     *
*        Terminate                                                    *
*                                                                     *
*---------------------------------------------------------------------*

         #STOP ,                   Return To Caller

*---------------------------------------------------------------------*
*                                                                     *
*        Constants And Work Area                                      *
*                                                                     *
*---------------------------------------------------------------------*

TESTMSG  DC    AL2(16),CL16'Test SYSMSG Text'

         #STARTWA ,
@TB723   DS    A
         #STOPWA ,

*---------------------------------------------------------------------*
*                                                                     *
*        DSECTs                                                       *
*                                                                     *
*---------------------------------------------------------------------*

         #DSECTS PSA,TCB,TCT,LCT
         END   ,
