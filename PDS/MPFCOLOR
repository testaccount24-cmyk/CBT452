MPFCOLOR TITLE '- CHANGE MESSAGE COLOR'
*---------------------------------------------------------------------*
*                                                                     *
*        Support Email Address: zOS.JES2@Gmail.com                    *
*                                                                     *
*---------------------------------------------------------------------*

*------------------------------------------------------------------*
*                                                                  *
*    SIMPLE MPF EXIT TO CHANGE THE COLOR (COLOUR FOR CANADIANS)    *
*    OF AN INDIVIDUAL MESSAGE.  SYS1.PARMLIB(MPFLST00) SYNTAX:     *
*                                                                  *
*        IEF404I,SUP(NO),USEREXIT(MPFCOLOR),AUTO(RED)              *
*                                                                  *
*------------------------------------------------------------------*
*        #DD03310 - ADD DELETABLE/NON-DELETABLE OPTION             *
*------------------------------------------------------------------*

         ACONTROL OPTABLE(ZS3)     Assembly OPTCDE Table
         SPLEVEL  SET=6            Specify OS/390 R2 macro format
         SYSSTATE ARCHLVL=2        Assembly requires z/Architecture
         SYSSTATE OSREL=ZOSV1R13   Assembly requires z/OS 1.13 & higher

MPFCOLOR #START ,                  START PROCEDURE                     *
               AMODE=CAP31,        EXECUTE IN 31-BIT ADDRESSING MODE   *
               RMODE=ANY,          EXECUTE IN ANY RESIDENCY MODE       *
               BASE=(R12),         DEFINE BASE REGISTER(S)             *
               REG1=(R1),          DEFINE PARAMETER REGISTER           *
               LEVEL=#V001R02      MAINTENANCE LEVEL

         L     R5,0(0,R1)          LOAD PARAMETER LIST ADDRESS
         USING CTXT,R5
         LA    R6,CTXTAUTT         A(AUTO TOKEN)

*---------------------------------------------------------------------*
*        CHECK "AUTO" TOKEN DELTABLE/NON-DELETABLE REQUEST.           *
*        - DELETABLE WILL BE DESCRIPTOR CODE 0.                       *
*        - NON-DELETALE WILL BE DESCRIPTOR CODE 2.                    *
*---------------------------------------------------------------------*
         CLI   CTXTAUTT,C'+'       MAKE NON-DELETABLE ?        #DD03310
         BNE   MPF00100            B. IF NOT                   #DD03310
         LA    R6,1(,R6)           SKIP OVER "+"               #DD03310
         OI    CTXTRFB1,CTXTRCDC   REQUEST DESC. CODE CHANGE   #DD03310
         L     R15,CTXTDCP         POINT TO DESCRIPTOR CODES   #DD03310
         USING CTXTDESC,R15                                    #DD03310
         MVI   CTXTDC1,CTXTDC02    CHANGE TO DESC=02           #DD03310
         MVI   CTXTDC2,0                                       #DD03310
         DROP  R15                                             #DD03310
         B     MPF00200            CONTINUE                    #DD03310

MPF00100 DS    0H                                              #DD03310
         CLI   CTXTAUTT,C'-'       MAKE DELETABLE ?            #DD03310
         BNE   MPF00200            B. IF NOT                   #DD03310
         LA    R6,1(,R6)           SKIP OVER "-"               #DD03310
         OI    CTXTRFB1,CTXTRCDC   REQUEST DESC. CODE CHANGE   #DD03310
         L     R15,CTXTDCP         POINT TO DESCRIPTOR CODES   #DD03310
         USING CTXTDESC,R15                                    #DD03310
         MVI   CTXTDC1,0           CHANGE TO DESC=00           #DD03310
         MVI   CTXTDC2,0                                       #DD03310
         DROP  R15                                             #DD03310
         B     MPF00200            CONTINUE                    #DD03310

*---------------------------------------------------------------------*
*        CHECK "AUTO" TOKEN FIELD FOR COLOR CHANGE REQUEST            *
*---------------------------------------------------------------------*
MPF00200 DS    0H
         LA    R3,COLORTAB         A(START OF COLOR TABLE)
MPF00300 DS    0H
         CLI   0(R3),255           CHECK FOR END OF TABLE
         BE    MPF00500            B. IF YES
         CLC   0(7,R6),0(R3)       CHECK FOR MATCHING TOKEN    #DD03010
         BE    MPF00400            B. IF SAME
         LA    R3,9(,R3)           A(NEXT ENTRY IN TABLE)
         B     MPF00300            KEEP SEARCHING

MPF00400 DS    0H
         OI    CTXTERF1,CTXTEMCO   REQUEST COLOR CHANGE
         MVC   CTXTCOLR,8(R3)      COPY REQUESTED OPTION
         B     MPFCEXIT

*---------------------------------------------------------------------*
*        CHECK "AUTO" TOKEN FIELD FOR HIGHLIGHTING CHANGE REQUEST     *
*---------------------------------------------------------------------*
MPF00500 DS    0H
         LA    R3,HILITTAB         A(START OF HILIGHT TABLE)
MPF00600 DS    0H
         CLI   0(R3),255           CHECK FOR END OF TABLE
         BE    MPF00800            B. IF YES
         CLC   0(7,R6),0(R3)       CHECK FOR MATCHING TOKEN    #DD03010
         BE    MPF00700            B. IF SAME
         LA    R3,9(,R3)           A(NEXT ENTRY IN TABLE)
         B     MPF00600            KEEP SEARCHING

MPF00700 DS    0H
         OI    CTXTERF1,CTXTEMHI   REQUEST HILIGHT CHANGE
         MVC   CTXTHILI,8(R3)      COPY REQUESTED OPTION
         B     MPFCEXIT

*---------------------------------------------------------------------*
*        CHECK "AUTO" TOKEN FIELD FOR INTENSITY CHANGE REQUEST        *
*---------------------------------------------------------------------*
MPF00800 DS    0H
         LA    R3,INTENTAB         A(START OF INTENSITY TABLE)
MPF00900 DS    0H
         CLI   0(R3),255           CHECK FOR END OF TABLE
         BE    MPFCEXIT            B. IF YES
         CLC   0(7,R6),0(R3)       CHECK FOR MATCHING TOKEN    #DD03010
         BE    MPF01000            B. IF SAME
         LA    R3,9(,R3)           A(NEXT ENTRY IN TABLE)
         B     MPF00900            KEEP SEARCHING

MPF01000 DS    0H
         OI    CTXTERF1,CTXTEMIN   REQUEST INTENSITY CHANGE
         MVC   CTXTINTN,8(R3)      COPY REQUESTED OPTION

*---------------------------------------------------------------------*
*        TERMINATE                                                    *
*---------------------------------------------------------------------*
MPFCEXIT #STOP RC=0                TERMINATE (ALWAYS RETURN CODE 0)

*---------------------------------------------------------------------*
*        OPTION TABLES                                                *
*---------------------------------------------------------------------*

COLORTAB DS    0H
         DC    CL8'BLUE    ',AL1(CTXTBLUE)
         DC    CL8'RED     ',AL1(CTXTRED)
         DC    CL8'PINK    ',AL1(CTXTPINK)
         DC    CL8'GREEN   ',AL1(CTXTGREE)
         DC    CL8'TURQ    ',AL1(CTXTTURQ)
         DC    CL8'YELLOW  ',AL1(CTXTYELO)
         DC    CL8'WHITE   ',AL1(CTXTWHIT)
         DC    9X'FF'

HILITTAB DS    0H
         DC    CL8'NONE    ',AL1(CTXTNOHI)
         DC    CL8'BLINK   ',AL1(CTXTBLNK)
         DC    CL8'REVERSE ',AL1(CTXTREVD)
         DC    CL8'UNDERLI ',AL1(CTXTUNDR)
         DC    9X'FF'

INTENTAB DS    0H
         DC    CL8'NORMAL  ',AL1(CTXTINOR)
         DC    CL8'HIGH    ',AL1(CTXTIHIG)
         DC    9X'FF'

*---------------------------------------------------------------------*
*        DSECTS                                                       *
*---------------------------------------------------------------------*

         IEZVX100 ,
         END   ,
