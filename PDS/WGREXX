/* REXX - to "front end" the WHOSGOT program.                        */
/*                                                                   */
/*  This REXX can be invoked on DLIST panel, or by TSO command with  */
/*  or without a data set name passed.                               */
/*  The REXX will not call WHOSGOT if the data set is not DASD       */
/*  (in some tests, tape mounts were issued - that not good), or     */
/*  does not exist; and if data set is VSAM, then test for component */
/*  in data set name, and remove it if found.                        */
/*                                                                   */
/*  Author: Greg Shirey of Ben E. Keith Company.                     */

Trace O
Address TSO

Arg dsn .
dsn = Strip(dsn)
DASD_DSN = 'N'

Do While dsn = ''                   /*  If no dsn passed, do this    */
  Say " "
  Say "Enter fully qualified data set name:"
  Pull Answer
  Answer = Strip(Answer)
  If Answer = "Q" Then Exit
  If Answer = "" then
     Say 'Q to exit, otherwise enter a data set name'
  else
     dsn = Strip(Answer)
End

ppos1 = pos("(",dsn)                 /* Check for '('              */
If ppos1 = 0 Then NOP                /* If none, go on             */
Else Do
  Say "WG-REXX - Member names or generation numbers not allowed"
  Exit 4
End

apos1 = pos("'",dsn)                 /* Check for apostrophe       */
If apos1 = 0 then dsn = "'"dsn"'"    /* Add them if not found      */

Rcode = LISTDSI(DSN)                /* Do a LISTDSI on the dsn     */
/* Uncomment for testing
  Say "SYSDSNAME" SYSDSNAME
  Say "SYSUNIT"   SYSUNIT
  Say "SYSRACFA"  SYSRACFA
  Say "SYSUNITS"  SYSUNITS
  Say "SYSREASON" SYSREASON
*/

If Rcode > 4 Then Do
  Say "WG-REXX - LISTDSI Return Code" Rcode "Reason Code" SYSREASON
  exit 4
End

If substr(SYSUNIT,1,2) = '33' | ,     /* 3390, 3380, 3350 (oldie) */
          SYSUNIT = 'SYSDA' | SYSUNIT = 'SYSALLDA' ,
  Then DASD_DSN = 'Y'

If DASD_DSN = 'N' Then Do             /* Tape or otherwise, leave */
  Say "WG-REXX" dsn "not on DASD or is uncataloged"
  Exit 4
End

If SYSDSORG = 'VS' Then Do
 Base = 'Full'                        /* Initialize variable      */
 Parts = TRANSLATE(dsn,' ','.')       /* Remove periods from dsn  */
 Count = Words(Parts)                 /* Count the dsn parts      */
 /* Remove component suffix's.  (.DATA .INDEX .D .I)              */
 If Word(Parts,Count) = "DATA'"
   Then Parse Var dsn Base ".DATA'"
 If Word(Parts,Count) = "D'"
   Then Parse Var dsn Base ".D'"
 If Word(Parts,Count) = "INDEX'"
   Then Parse Var dsn Base ".INDEX'"
 If Word(Parts,Count) = "I'"
   Then Parse Var dsn Base ".I'"
 If Base = 'Full' Then NOP
 Else dsn = Base||"'"                 /* Replace dsn with the VSAM*/
End                                   /* Base cluster name and '  */

Exist = SYSDSN(dsn)                  /* check dsn existance        */
IF Exist = 'OK' | Exist = 'UNAVAILABLE DATASET' | ,
  Exist = 'PROTECTED DATASET' Then NOP
  Else do
    Say "WG-REXX" Exist "-" dsn      /* If no, get out             */
    Exit
end

If Answer <> 'ANSWER' Then              /* Info message for      */
   Say "Calling WHOSGOT program....."   /*  TSO calls            */

"WHOSGOT "dsn                        /* call WHOSGOT program      */
Rcode = rc                           /*       save return code    */

Exit Rcode
