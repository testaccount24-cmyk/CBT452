RETCODE  TITLE '- GENERATE RETURN CODE/ABEND BASED ON PARM='
*---------------------------------------------------------------------*
*                                                                     *
*        Support Email Address: zOS.JES2@Gmail.com                    *
*                                                                     *
*---------------------------------------------------------------------*

         ACONTROL OPTABLE(ZS3)     ASSEMBLY OPTCDE TABLE
         SPLEVEL  SET=6            SPECIFY OS/390 R2 MACRO FORMAT
         SYSSTATE ARCHLVL=2        ASSEMBLY REQUIRES Z/ARCHITECTURE
         SYSSTATE OSREL=ZOSV1R13   ASSEMBLY REQUIRES Z/OS 1.13 & HIGHER

RETCODE  #START WKDSECT=WORKAREA,APARS=,AMODE=CAP24,                   *
               COPY=YES,                                               *
               LEVEL=#V001R01

*---------------------------------------------------------------------*
*                                                                     *
*        CONSTRUCT COMMAND BUFFER FROM THE PARAMETER LIST             *
*                                                                     *
*---------------------------------------------------------------------*

         L     R1,0(,R1)           >>---> PARM FIELD
         SR    R2,R2
         ICM   R2,3,0(R1)          LOAD PARAMETER LENGTH
         BZ    RETC0100            B. IF NO PARAMETER SPECIFIED

         #EXEC -R2,MVC,COMMAND+8(*-*),2(R1)
         LA    R2,9(,R2)           CALCULATE LENGTH OF COMMAND BUFFER
         STH   R2,COMMAND          RESET LENGTH OF COMMAND BUFFER

RETC0100 DS    0H
         MVC   PPLPCL-PPL+WRKPPL,=A(PARSEPCL)
         LA    R0,PDLADDR          >>---> IKJPARS ANSWER PLACE
         ST    R0,PPLANS-PPL+WRKPPL
         LA    R0,COMMAND          >>---> INPUT COMMAND BUFFER
         ST    R0,PPLCBUF-PPL+WRKPPL
         ST    R0,CSPLCBUF-CSPL+WRKCSPL
         ST    R13,PPLUWA-PPL+WRKPPL

*---------------------------------------------------------------------*
*                                                                     *
*        SCAN & PARSE THE COMMAND BUFFER                              *
*                                                                     *
*---------------------------------------------------------------------*

         LA    R1,WRKCSPL          >>---> SCAN PARAMETER LIST
         LA    R0,4                INDICATE "IKJSCAN" FUNCTION
         L     R15,=V(TSOPARSE)
         #BALR R14,R15

         LA    R1,WRKPPL           >>---> PARSE PARAMETER LIST
         LA    R0,0                INDICATE "IKJPARS" FUNCTION
         L     R15,=V(TSOPARSE)
         #BALR R14,R15
         #SETRC (R15)              SET RETURN CODE

*---------------------------------------------------------------------*
*                                                                     *
*        ISSUE MESSAGES TRAPPED BY PARSE SVC                          *
*                                                                     *
*---------------------------------------------------------------------*

         LTR   R3,R1               CHECK IF ANY MESSAGES RETURNED
         BZ    RETC0400            B. IF NO

RETC0200 DS    0H
         MVC   WTOBUFFR+4(80),BLANKS
         LH    R2,4(,R3)           LOAD MESSAGE TEXT LENGTH
         CH    R2,=H'80'           CHECK FOR MAXIMUM LENGTH
         BNH   RETC0300            B. IF NOT HIGHER THAN 80
         LH    R2,=H'80'           SET TO MAXIMUM LENGTH
RETC0300 DS    0H
         #EXEC -R2,MVC,WTOBUFFR+4(*-*),8(R3)

         WTO   MF=(E,WTOBUFFR)     ISSUE ERROR MESSAGE

         ICM   R3,15,0(R3)         >>---> NEXT MESSAGE
         BNZ   RETC0200            B. IF MORE MESSAGES

RETC0400 DS    0H
         ICM   R15,15,RETRCODE     CHECK IKJPARS RETURN CODE
         BZ    RETC0450            B. IF SUCCESSFUL PARSE
         WTO   'RETC001I INVALID PARAMETER LIST',ROUTCDE=11
         B     RETCEXIT

*---------------------------------------------------------------------*
*                                                                     *
*        SET RETURN CODE TO REQUESTED VALUE                           *
*                                                                     *
*---------------------------------------------------------------------*

RETC0450 DS    0H
         L     R10,PDLADDR         LOAD PDL ADDRESS
         USING IKJPARMD,R10

         CLI   TYPEKW+1,1          CHECK FOR "RC(...)"
         BNE   RETC0500            B. IF NOT
         L     R1,RC               LOAD REQUESTED RETURN CODE ADDRESS
         L     R15,0(,R1)          LOAD REQUESTED RETURN CODE
         #SETRC (R15),FORCE=Y      SET RETURN CODE
         B     RETCEXIT

RETC0500 DS    0H
         CLI   TYPEKW+1,2          CHECK FOR "USER(...)"
         BNE   RETC0600            B. IF NOT
         L     R1,USER             LOAD REQUESTED USER CODE ADDRESS
         L     R15,0(,R1)          LOAD REQUESTED USER CODE
         ABEND (15)
RETC0600 DS    0H
         CLI   TYPEKW+1,3          CHECK FOR "SYSTEM(...)"
         BNE   RETCEXIT            B. IF NOT
         L     R1,SYSTEM           LOAD REQUESTED SYSTEM CODE ADDRESS
         SLR   R15,R15
         ICM   R15,1,0(R1)         LOAD 1 BYTE SYSTEM CODE
         CLC   =X'0001',SYSTEM+4   CHECK FOR 1 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,3,0(R1)         LOAD 2 BYTE SYSTEM CODE
         CLC   =X'0002',SYSTEM+4   CHECK FOR 2 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,7,0(R1)         LOAD 3 BYTE SYSTEM CODE
         CLC   =X'0003',SYSTEM+4   CHECK FOR 3 BYTE CODE
         BE    RETC0700            B. IF YES
         ICM   R15,15,0(R1)        LOAD 4 BYTE SYSTEM CODE
RETC0700 DS    0H
         ABEND (15),,,SYSTEM
         DROP  R10

*---------------------------------------------------------------------*
*                                                                     *
*        RETURN TO CALLER                                             *
*                                                                     *
*---------------------------------------------------------------------*

RETCEXIT DS    0H
         IKJRLSA PDLADDR           RELEASE THE IKJPARS BUFFERS
         #STOP ,                   RETURN TO CALLER

*---------------------------------------------------------------------*
*                                                                     *
*        WORK AREA                                                    *
*                                                                     *
*---------------------------------------------------------------------*

BLANKS   DC    CL256' '            LOTS OF BLANKS
COMMAND  DS    0F
         DC    AL2(7,0),CL104'XXX '
WTOBUFFR WTO   '---------+---------+---------+---------+---------+-----*
               ----+---------+---------+',ROUTCDE=11,MF=L

         #STARTWA ,
PDLADDR  DS    F                   IKJPARS ANSWER PLACE
WRKCSPL  DS    CL(CSPLLEN)         SCAN PARAMETER LIST
WRKPPL   DS    CL(PPLLEN)          PARSE PARAMETER LIST
         #STOPWA ,

*---------------------------------------------------------------------*
*                                                                     *
*        COMMAND OPERANDS                                             *
*                                                                     *
*---------------------------------------------------------------------*

         PRINT NOGEN
PARSEPCL IKJPARM ,

TYPEKW   IKJKEYWD DEFAULT='RC(0)'
         IKJNAME 'RC',SUBFLD=RCSUB
         IKJNAME 'USER',SUBFLD=USERSUB
         IKJNAME 'SYSTEM',SUBFLD=SYSSUB

RCSUB    IKJSUBF
RC       IKJIDENT 'RC',                                                *
               INTEG,                                                  *
               MAXLNTH=4,                                              *
               PROMPT='RETURN CODE'

USERSUB  IKJSUBF
USER     IKJIDENT 'USER',                                              *
               INTEG,                                                  *
               MAXLNTH=3,                                              *
               PROMPT='USER ABEND CODE'

SYSSUB   IKJSUBF
SYSTEM   IKJIDENT 'SYSTEM',                                            *
               HEX,                                                    *
               MAXLNTH=3,                                              *
               PROMPT='SYSTEM ABEND CODE'

         IKJENDP
         PRINT GEN

         PRINT NOGEN
*              'PARSE PARAMETER LIST'
         IKJPPL ,
PPLLEN   EQU   *-PPL               LENGTH OF PPL
*              'SCAN PARAMETER LIST'
         IKJCSPL ,
CSPLLEN  EQU   *-CSPL              LENGTH OF CSPL
         END   ,
